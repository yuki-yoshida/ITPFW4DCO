require SET SET.cafe

module! SERVICE {
 protecting(NAT)
 [LocalState Service]
 ops closed open ready : -> LocalState {constr}
 op sv : Nat LocalState -> Service {constr}
}

module! SV-SET {
 protecting(
  SET(SERVICE{sort Elt -> Service})
   * {sort Set -> SvSet,
      op empty -> empSvSet})
}

open SV-SET .
 reduce #(sv(1,closed) sv(2,open)) . -- to 2.

 op svs : -> SvSet .
 reduce #(sv(1,closed) svs) = # svs + 1 . -- to true.
close

module! UPDATE {
 using(SV-SET)

 [State]

 op < _ > : SvSet -> State {constr}

 var SVS : SvSet
 var N : Nat

 trans [c2o]: 
  < sv(N,closed) SVS > => < sv(N,open) SVS > .

 ctrans [o2r]: 
  < sv(N,open) SVS > => < sv(N,ready) SVS >
  if # SVS > 0 .
}

select UPDATE .
 execute < sv(1,closed) sv(2,open) > .
   -- to < sv(1,ready)  sv(2,ready) > .
 execute < sv(1,closed) > .
   -- to < sv(1,open) > .

select UPDATE .
 reduce < sv(1,closed) sv(2,open) > =(*,1)=>+ < SVS > .
   -- to true.
 reduce < sv(3,closed) sv(4,ready) > =(*,1)=>+ < SVS > .
   -- to true.
 reduce < sv(5,open) > =(*,1)=>+ < SVS > .
   -- to false.

open UPDATE .
 pred anyOpen : SvSet .
   -- The same as: op anyOpen : SvSet -> Bool .
 eq anyOpen(sv(N,open) SVS) = true .
 var CC : Bool .
 reduce 
  < sv(1,closed) sv(2,open) > =(*,1)=>+ < SVS > if CC
    suchThat CC implies anyOpen(SVS) { true } . -- to true.
 reduce not (
  < sv(1,closed) sv(2,open) > =(*,1)=>+ < SVS > if CC
    suchThat not ((CC implies anyOpen(SVS)) == true) { true } ) . -- to false.

 reduce anyOpen(sv(1,closed)) == true . -- to false.
 reduce anyOpen(sv(1,closed)) = true .  -- to anyOpen(sv(1,closed)) = true .

close

module ProofUPDATE {
 protecting(UPDATE)
 -- Theorem to be proved.
 pred theorem : Nat LocalState Nat LocalState SvSet

 vars N N1 N2 : Nat  vars S1 S2 : LocalState .
 var SVS : SvSet     var SS : State

 eq theorem(N1,S1,N2,S2,SVS)
   = ((S1 == ready) = false and (S2 == ready) = false)
     implies < sv(N1,S1) sv(N2,S2) SVS > =(*,1)=>+ SS .

 eq (1 + N > 0) = true . -- Axiom of Nat

 -- Arbitrary constants.
 ops s1 s2 : -> LocalState
 ops n1 n2 : -> Nat op svs : -> SvSet}

-- The most general case.
open ProofUPDATE .
 reduce theorem(n1,s1,n2,s2,svs) . -- to false.
close

-- Case 1: Both services are closed.
open ProofUPDATE .
 eq s1 = closed .
 eq s2 = closed .
 reduce theorem(n1,s1,n2,s2,svs) . -- to true.
close

-- Case 2: Both services are open.
open ProofUPDATE .
 eq s1 = open .
 eq s2 = open .
 reduce theorem(n1,s1,n2,s2,svs) . -- to true.
close

-- Case 3: A closed service and an open service.
open ProofUPDATE .
 eq s1 = closed .
 eq s2 = open .
 reduce theorem(n1,s1,n2,s2,svs) . -- to true.
close

select ProofUPDATE .
:goal { eq theorem(n1,s1,n2,s2,svs) = true . }
:def csp-s1 = :csp {
 eq s1 = closed .  eq s1 = open .  eq s1 = ready . }
:def csp-s2 = :csp {
 eq s2 = closed .  eq s2 = open .  eq s2 = ready . }
:apply (csp-s1 csp-s2 rd)
describe proof
show proof

