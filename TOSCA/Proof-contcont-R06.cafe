require ProofContCont Proof-contcont.cafe

--> **********************************************************
--> (2) inv(S) and not final(S) implies cont(SS) or final(SS).
--> **********************************************************

--> ***************************************************************************
-->  ctrans [R06]:
-->     < SetND, (cap(dependsOn,IDCP,open,     IDND) SetCP), SetRQ, SetRL, MP >
-->  => < SetND, (cap(dependsOn,IDCP,available,IDND) SetCP), SetRQ, SetRL, MP >
-->     if state(getNode(SetND,IDND)) = started .
--> ***************************************************************************
select ProofContCont .
-- When R06 can be applied to the state S:
--> *************************************************************************
--> Step 2-1: Begin with the cases each of which matches to LHS of each rule.
--> *************************************************************************
:goal {
  eq contcont(< sND, (cap(dependsOn,idCP,open,idND) sCP), sRQ, sRL, mp >) = true .
}
--> ***************************************************************
--> Step 2-8: When there is a dangling link, split the current case
-->  into cases where the linked object does or does not exist.
--> ***************************************************************
-- The node of the capability of idCP does or does not exist.
:def csp2-8idNDa = :csp {
  eq existND(sND,idND) = false .
  eq sND = (node(tnd,idND,snd) sND') .
}
:def csp2-8idNDb = :csp {
  eq snd = initial .
  eq snd = created .
  eq snd = started .
}
:apply (csp2-8idNDa rd- csp2-8idNDb rd-)
-- The node is started:
--> *********************************************************
--> Step 2-3: Split the current case into cases where
-->  predicate final does or does not hold in the next state.
--> *********************************************************
-- We know that final(SS) never holds in this case.
--> ***************************************************************
--> Step 2-4: Consider which rule can be applied to the next state.
--> ***************************************************************
-- The next rule should be R08 for the capability of idCP.
--> *************************************************************************************
--> Step 2-5: Split the current case into cases which collectively cover the current case
-->  and the next state of one of the split cases matches to LHS of the current rule.
--> *************************************************************************************
-- LHS of R08 requires a corresponding relationship.
-- There is or is not a corresponding relationship.
:def csp2-5a = :csp {
  eq onlyOneRLOfCP(sRL,idCP) = false .
  eq sRL = (rel(trl,idRL,idCP,idRQ) sRL') .
}
:def csp2-5b = :csp {
  eq trl = hostedOn .
  eq trl = dependsOn .
  eq trl = connectsTo .
}
:apply (csp2-5a rd- csp2-5b rd-)
-- When the relationship is dependsOn:
-- There is or is not a corresponding requirement.
:def csp2-8idRQa = :csp {
  eq existRQ(sRQ,idRQ) = false .
  eq sRQ = (req(trl',idRQ,srq,idND') sRQ') .
}
:def csp2-8idRQb = :csp {
  eq trl' = hostedOn .
  eq trl' = dependsOn .
  eq trl' = connectsTo .
}
:apply (csp2-8idRQa rd- csp2-8idRQb rd-)
-- When the requirement is dependsOn:
-- The requirement is unbound, waiting, or ready.
:def cspsrq = :csp {
  eq srq = unbound .
  eq srq = waiting .
  eq srq = ready .
}
:apply (cspsrq rd-)
-- When the requirement is unbound:
--> ***************************************************************
--> Step 2-4: Consider which rule can be applied to the next state.
--> ***************************************************************
-- The next rule should be R07 for the requirement of idRQ.
--> ************************************************************
--> Step 2-7: When there are two identifier constants, split the
--> current case into cases where they are or are not the same. 
--> ************************************************************
-- The node of the requirement of idRQ does or does not exist.
-- idND and idND' are or are not the same.
:def ctfidND' = :ctf {
  eq idND' = idND .
}
:apply (ctfidND' rd-)
-- When idND and idND' are not the same.
--> ***************************************************************
--> Step 2-8: When there is a dangling link, split the current case
-->  into cases where the linked object does or does not exist.
--> ***************************************************************
-- The node of the requirement of idRQ does or does not exist.
:def csp2-8idND'a = :csp {
  eq existND(sND',idND') = false .
  eq sND' = (node(tnd',idND',snd') sND'') .
}
:def csp2-8idND'b = :csp {
  eq snd' = initial .
  eq snd' = created .
  eq snd' = started .
}
:apply (csp2-8idND'a rd- csp2-8idND'b rd-)
show proof
select
