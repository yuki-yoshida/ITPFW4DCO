require PROOF Proof.cafe

--> Common lemmas in this domain.

module TOSCALEMMA {
  protecting(PROOF)

  vars IDCP IDCP1 IDCP2 : CPID
  var IDND : NDID
  var IDRL : RLID
  var IDRQ : RQID
  vars SRQ SRQ1 SRQ2 : RQState
  vars SCP SCP1 SCP2 : CPState
  var STCP : SetOfCPState
  var STRQ : SetOfRQState
  var SetCP : SetOfCapability
  var SetRL : SetOfRelationship
  var SetRQ : SetOfRequirement
  var TRL : RLType
  var MP : PoolOfMsg

  -- ifConnectsToCPInStatesThenRQInStatesOrOpenMsg keeps to hold
  -- after changing the state of a requirement 
  -- whose corresponding capability is not included in the set of capabilities.
  -- pred tosca-lemma01 : SetOfCapability
  --                RQState RQState NDID CPID RQID RLID RLType 
  --                SetOfCPState SetOfRQState
  --                SetOfRequirement SetOfRelationship PoolOfMsg 
  -- eq tosca-lemma01(SetCP,SRQ1,SRQ2,IDND,IDCP,IDRQ,IDRL,TRL,STCP,STRQ,SetRQ,SetRL,MP)
  --    = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
  --               SetCP,STCP,(req(TRL,IDRQ,SRQ1,IDND) SetRQ),STRQ,
  -- 		(rel(TRL,IDRL,IDCP,IDRQ) SetRL),MP) implies
  --      ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
  --               SetCP,STCP,(req(TRL,IDRQ,SRQ2,IDND) SetRQ),STRQ,
  -- 		(rel(TRL,IDRL,IDCP,IDRQ) SetRL),MP)
  --    when not existCP(SetCP,IDCP) and uniqCP(SetCP) and
  --         allCPHaveRL(SetCP,(rel(TRL,IDRL,IDCP,IDRQ) SetRL)) and
  --         allRLHaveRQ((rel(TRL,IDRL,IDCP,IDRQ) SetRL),(req(TRL,IDRQ,SRQ2,IDND) SetRQ)) .

  pred tosca-lemma02 : SetOfRelationship RLType CPID CPState NDID SetOfCapability SetOfRequirement
  eq tosca-lemma02(SetRL,TRL,IDCP,SCP,IDND,SetCP,SetRQ)
     = allRLHaveSameTypeCPRQ(SetRL,(cap(TRL,IDCP,SCP,IDND) SetCP),SetRQ) implies
       allRLHaveSameTypeCPRQ(SetRL,SetCP,SetRQ)
       when uniqCP((cap(TRL,IDCP,SCP,IDND) SetCP)) .

  -- ifConnectsToCPInStatesThenRQInStatesOrOpenMsg keeps to hold
  -- after adding an open message.
  pred tosca-lemma03 : SetOfCapability SetOfCPState SetOfRequirement SetOfRQState 
                         SetOfRelationship PoolOfMsg CPID
  eq tosca-lemma03(SetCP,STCP,SetRQ,STRQ,SetRL,MP,IDCP)
     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP) implies
       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,(opMsg(IDCP) MP)) .

  -- ifConnectsToCPInStatesThenRQInStatesOrOpenMsg keeps to hold
  -- after adding an available message.
  pred tosca-lemma04 : SetOfCapability SetOfCPState SetOfRequirement SetOfRQState 
                         SetOfRelationship PoolOfMsg CPID
  eq tosca-lemma04(SetCP,STCP,SetRQ,STRQ,SetRL,MP,IDCP)
     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP) implies
       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,(avMsg(IDCP) MP)) .

  -- ifConnectsToCPInStatesThenRQInStatesOrOpenMsg keeps to hold
  -- after changing the state of a requirement to the target state.
  pred tosca-lemma05 : SetOfCapability SetOfCPState SetOfRequirement RQState RQState 
       		       	 SetOfRQState SetOfRelationship PoolOfMsg RQID NDID CPID
  eq tosca-lemma05(SetCP,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDCP)
     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
		SetCP,STCP,(req(connectsTo,IDRQ,SRQ1,IDND) SetRQ),STRQ,
		SetRL,(opMsg(IDCP) MP)) implies
       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
		SetCP,STCP,(req(connectsTo,IDRQ,SRQ2,IDND) SetRQ),STRQ,
		SetRL,MP) 
     when (SRQ2 in STRQ) and (IDRQ = req(getRLOfCP(SetRL, IDCP))) .

  pred tosca-lemma06 : SetOfCapability RLType SetOfCPState SetOfRequirement RQState RQState 
       		       	 SetOfRQState SetOfRelationship PoolOfMsg RQID NDID RLID
  eq tosca-lemma06(SetCP,TRL,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDRL)
     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
		SetCP,STCP,(req(TRL,IDRQ,SRQ1,IDND) SetRQ),STRQ,
		SetRL,MP) implies
       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
		SetCP,STCP,(req(TRL,IDRQ,SRQ2,IDND) SetRQ),STRQ,
		SetRL,MP)
     when not (TRL = connectsTo) and 
          uniqCP(SetCP) and
          allCPHaveRL(SetCP,SetRL) and
          allRLHaveRQ(SetRL,SetRQ) and
          allRLHaveSameTypeCPRQ(SetRL,SetCP,(req(TRL,IDRQ,SRQ2,IDND) SetRQ)) .

  -- ifConnectsToCPInStatesThenRQInStatesOrOpenMsg keeps to hold
  -- after changing the state of a requirement to the target state.
  pred tosca-lemma07 : SetOfCapability SetOfCPState SetOfRequirement RQState RQState 
       		       	 SetOfRQState SetOfRelationship PoolOfMsg RQID NDID CPID
  eq tosca-lemma07(SetCP,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDCP)
     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
		SetCP,STCP,(req(connectsTo,IDRQ,SRQ1,IDND) SetRQ),STRQ,
		SetRL,(avMsg(IDCP) MP)) implies
       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
		SetCP,STCP,(req(connectsTo,IDRQ,SRQ2,IDND) SetRQ),STRQ,
		SetRL,MP) 
     when SRQ2 in STRQ .

  pred tosca-lemma08 : SetOfCapability
                       CPState SetOfCPState SetOfRQState SetOfRequirement SetOfRelationship PoolOfMsg
  eq tosca-lemma08(SetCP,SCP,STCP,STRQ,SetRQ,SetRL,MP)
     = allCPInStates(SetCP,SCP) 
       implies ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP)
     when not (SCP in STCP) .

  pred tosca-lemma09 : SetOfCapability
                       SetOfCPState SetOfRequirement SetOfRQState SetOfRelationship PoolOfMsg CPID
  eq tosca-lemma09(SetCP,STCP,SetRQ,STRQ,SetRL,MP,IDCP)
     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP) 
       implies ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,(opMsg(IDCP) MP)) .

  pred tosca-lemma10 : SetOfCapability SetOfCPState
                         SetOfRequirement SetOfRQState SetOfRelationship PoolOfMsg CPID
  eq tosca-lemma10(SetCP,STCP,SetRQ,STRQ,SetRL,MP,IDCP)
     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP) 
       implies ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,(avMsg(IDCP) MP)) .

  pred tosca-lemma11 : SetOfCapability SetOfCPState SetOfRequirement RQState RQState 
       		       	 SetOfRQState SetOfRelationship PoolOfMsg RQID NDID CPID
  eq tosca-lemma11(SetCP,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDCP)
     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
		SetCP,STCP,(req(connectsTo,IDRQ,SRQ1,IDND) SetRQ),STRQ,
		SetRL,(opMsg(IDCP) MP)) implies
       ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
		SetCP,STCP,(req(connectsTo,IDRQ,SRQ2,IDND) SetRQ),STRQ,
		SetRL,MP) 
     when not (SRQ1 in STRQ) .

  pred tosca-lemma12 : SetOfCapability
                       RLType SetOfCPState SetOfRequirement RQState RQState 
       		       SetOfRQState SetOfRelationship PoolOfMsg RQID NDID
  eq tosca-lemma12(SetCP,TRL,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND)
     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
		SetCP,STCP,(req(TRL,IDRQ,SRQ1,IDND) SetRQ),STRQ,SetRL,MP) implies
       ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
		SetCP,STCP,(req(TRL,IDRQ,SRQ2,IDND) SetRQ),STRQ,SetRL,MP)
     when not (TRL = connectsTo) and 
          uniqCP(SetCP) and
          allCPHaveRL(SetCP,SetRL) and
          allRLHaveRQ(SetRL,SetRQ) and
          allRLHaveSameTypeCPRQ(SetRL,SetCP,(req(TRL,IDRQ,SRQ2,IDND) SetRQ)) .

  pred tosca-lemma13 : SetOfCapability
                       CPState SetOfCPState SetOfRQState SetOfRequirement SetOfRelationship PoolOfMsg
  eq tosca-lemma13(SetCP,SCP,STCP,STRQ,SetRQ,SetRL,MP)
     = allCPInStates(SetCP,SCP) 
       implies ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP)
     when not (SCP in STCP) .

  pred tosca-lemma14 : SetOfCapability SetOfCPState SetOfRequirement RQState RQState 
       		       	 SetOfRQState SetOfRelationship PoolOfMsg RQID NDID CPID
  eq tosca-lemma14(SetCP,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDCP)
     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
		SetCP,STCP,(req(connectsTo,IDRQ,SRQ1,IDND) SetRQ),STRQ,
		SetRL,(avMsg(IDCP) MP)) implies
       ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
		SetCP,STCP,(req(connectsTo,IDRQ,SRQ2,IDND) SetRQ),STRQ,
		SetRL,MP) 
     when (SRQ2 in STRQ) and (IDRQ = req(getRLOfCP(SetRL, IDCP))) .

  pred tosca-lemma15 : PoolOfMsg SetOfCapability CPID NDID RLType CPState CPState SetOfCPState
  eq tosca-lemma15(MP,SetCP,IDCP,IDND,TRL,SCP1,SCP2,STCP)
     = ifOpenMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP1,IDND) SetCP),STCP) implies
       ifOpenMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP2,IDND) SetCP),STCP) 
     when (SCP2 in STCP) .

  pred tosca-lemma16 : PoolOfMsg SetOfCapability CPID NDID RLType CPState CPState SetOfCPState
  eq tosca-lemma16(MP,SetCP,IDCP,IDND,TRL,SCP1,SCP2,STCP)
     = ifOpenMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP1,IDND) SetCP),STCP) implies
       ifOpenMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP2,IDND) SetCP),STCP) 
     when not (SCP1 in STCP) .

  -- ifAvailableMsgThenCPInStates keeps to hold
  -- after changing the state of a capability to the target state. 
  pred tosca-lemma17 : PoolOfMsg SetOfCapability CPID NDID RLType CPState CPState SetOfCPState
  eq tosca-lemma17(MP,SetCP,IDCP,IDND,TRL,SCP1,SCP2,STCP)
     = ifAvailableMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP1,IDND) SetCP),STCP) implies
       ifAvailableMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP2,IDND) SetCP),STCP)
     when (SCP2 in STCP) .

  -- ifAvailableMsgThenCPInStates keeps to hold
  -- after changing the state of a capability whose current state is not the target state. 
  pred tosca-lemma18 : PoolOfMsg SetOfCapability CPID NDID RLType CPState CPState SetOfCPState
  eq tosca-lemma18(MP,SetCP,IDCP,IDND,TRL,SCP1,SCP2,STCP)
     = ifAvailableMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP1,IDND) SetCP),STCP) implies
       ifAvailableMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP2,IDND) SetCP),STCP)
     when not (SCP1 in STCP) .

  pred tosca-lemma19 : SetOfCapability
                       RQState SetOfRQState SetOfCPState SetOfRequirement SetOfRelationship
  eq tosca-lemma19(SetCP,SRQ,STRQ,STCP,SetRQ,SetRL)
     = (allRQInStates(SetRQ,SRQ) and 
        allCPHaveRL(SetCP,SetRL) and allRLHaveRQ(SetRL,SetRQ)) implies
       ifCPInStatesThenRQInStates(SetCP,STCP,SetRQ,STRQ,SetRL) 
     when SRQ in STRQ .

  -- toscal-lemma02 is a sub-lemma for lemma06 and lemma12
  ceq [tosca-lemma02 :nonexec]:
      (allRLHaveSameTypeCPRQ(SetRL,(cap(TRL,IDCP,SCP,IDND) SetCP),SetRQ) and
       allRLHaveSameTypeCPRQ(SetRL,SetCP,SetRQ) )
     = allRLHaveSameTypeCPRQ(SetRL,(cap(TRL,IDCP,SCP,IDND) SetCP),SetRQ)
     if uniqCP((cap(TRL,IDCP,SCP,IDND) SetCP)) .
}

--> *********************************************************************************************
--> Proof of tosca-lemma01.
-->  eq tosca-lemma01(SetCP,SRQ1,SRQ2,IDND,IDCP,IDRQ,IDRL,TRL,STCP,STRQ,SetRQ,SetRL,MP)
-->     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,
-->                                                     (req(TRL,IDRQ,SRQ1,IDND) SetRQ),STRQ,
-->					             (rel(TRL,IDRL,IDCP,IDRQ) SetRL),MP) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,
-->                                                     (req(TRL,IDRQ,SRQ2,IDND) SetRQ),STRQ,
-->						     (rel(TRL,IDRL,IDCP,IDRQ) SetRL),MP)
-->     when not existCP(SetCP,IDCP) and uniqCP(SetCP) and
-->          allCPHaveRL(SetCP,(rel(TRL,IDRL,IDCP,IDRQ) SetRL)) and
-->          allRLHaveRQ((rel(TRL,IDRL,IDCP,IDRQ) SetRL),(req(TRL,IDRQ,SRQ2,IDND) SetRQ)) .
--> *********************************************************************************************
-- select TOSCALEMMA
-- :goal {
--   eq tosca-lemma01(empCP,
-- 		   srq,srq',idND,idCP,idRQ,idRL,trl,stCP,stRQ,sRQ,sRL,mp) = true . 
-- }
-- :apply (rd-)
-- show proof
-- :goal {
--   eq (tosca-lemma01(sCP,
-- 		    srq,srq',idND,idCP,idRQ,idRL,trl,stCP,stRQ,sRQ,sRL,mp) implies
--       tosca-lemma01((cap(trl',idCP',scp,idND') sCP),
-- 		    srq,srq',idND,idCP,idRQ,idRL,trl,stCP,stRQ,sRQ,sRL,mp)) = true .
-- }
-- :def ctfidCP' = :ctf { 
--   eq idCP' = idCP .
-- }
-- -- Since :apply(xxx rd-) is too time consuming, we use :apply(xxx):appy(rd) instead.
-- -- :apply (ctfidCP' rd-)
-- :apply (ctfidCP')
-- :apply (rd)
-- :def ctfidCP = :ctf { 
--   eq existCP(sCP,idCP) = true .
-- }
-- -- Since :apply(xxx rd-) is too time consuming, we use :apply(xxx):appy(rd) instead.
-- -- :apply (ctfidCP rd-)
-- :apply (ctfidCP)
-- :apply (rd)
-- :def ctfsCPa = :ctf { 
--   eq uniqCP(sCP) = true .
-- }
-- :def ctfsCPb = :ctf {
--   eq existCP(sCP,idCP') = true .
-- }
-- :apply (ctfsCPa ctfsCPb rd-)
-- :def ctfsRLa = :ctf { 
--   eq allCPHaveRL(sCP,(rel(trl,idRL,idCP,idRQ) sRL)) = true .
-- }
-- :apply (ctfsRLa rd-)
-- :def ctfsRLb = :ctf { 
--   eq allRLHaveRQ(sRL,(req(trl,idRQ,srq',idND) sRQ)) = true .
-- }
-- :apply (ctfsRLb rd-)
-- :def cspsRL = :csp {
--   eq existCP(sRL,idCP') = false .
--   eq sRL = (rel(trl',idRL',idCP',idRQ') sRL') .
-- }
-- -- Since :apply(xxx rd-) is too time consuming, we use :apply(xxx):appy(rd) instead.
-- -- :apply (cspsRL rd-)
-- :apply (cspsRL)
-- :apply (rd)
-- :def ctfidRQ' = :ctf {
--   eq idRQ' = idRQ .
-- }
-- :apply (ctfidRQ' rd-)
-- show proof
-- select

--> Proof of tosca-lemma02.
--> *******************************************************************************
-->  eq tosca-lemma02(SetRL,TRL,IDCP,SCP,IDND,SetCP,SetRQ)
-->     = allRLHaveSameTypeCPRQ(SetRL,(cap(TRL,IDCP,SCP,IDND) SetCP),SetRQ) implies
-->       allRLHaveSameTypeCPRQ(SetRL,SetCP,SetRQ) .
-->     when uniqCP((cap(TRL,IDCP,SCP,IDND) SetCP)) .
--> *******************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma02(empRL,trl,idCP,scp,idND,sCP,sRQ) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma02(sRL,trl,idCP,scp,idND,sCP,sRQ) implies
      tosca-lemma02((rel(trl',idRL,idCP',idRQ) sRL),trl,idCP,scp,idND,sCP,sRQ)) = true .
}
:def ctfidCP' = :ctf { 
  eq idCP' = idCP .
}
:apply (ctfidCP' rd-)
show proof
select

--> Proof of tosca-lemma03.
--> ***************************************************************************************************
-->  eq tosca-lemma03(SetCP,STCP,SetRQ,STRQ,SetRL,MP,IDCP)
-->     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,(opMsg(IDCP) MP)) .
--> ***************************************************************************************************
select TOSCALEMMA
:goal {  
  eq tosca-lemma03(empCP,stCP,sRQ,stRQ,sRL,mp,idCP) = true .
}
:apply (rd-)
show proof
:goal {  
  eq (tosca-lemma03(sCP,stCP,sRQ,stRQ,sRL,mp,idCP) implies 
      tosca-lemma03((cap(trl,idCP',scp,idND) sCP),stCP,sRQ,stRQ,sRL,mp,idCP)) = true .
}
:def ctfidCP' = :ctf {
  eq idCP' = idCP .
}
:apply (ctfidCP' rd-)
show proof
select

--> Proof of tosca-lemma04.
--> ***************************************************************************************************
-->  eq tosca-lemma04(SetCP,STCP,SetRQ,STRQ,SetRL,MP,IDCP)
-->     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,(avMsg(IDCP) MP)) .
--> ***************************************************************************************************
select TOSCALEMMA .
:goal {  
  eq tosca-lemma04(empCP,stCP,sRQ,stRQ,sRL,mp,idCP) = true .
}
:apply (rd-)
show proof
:goal {  
  eq (tosca-lemma04(sCP,stCP,sRQ,stRQ,sRL,mp,idCP) implies 
      tosca-lemma04((cap(trl,idCP',scp,idND) sCP),stCP,sRQ,stRQ,sRL,mp,idCP)) = true .
}
:def ctfidCP' = :ctf {
  eq idCP' = idCP .
}
:apply (ctfidCP' rd-)
show proof
select

--> Proof of tosca-lemma05.
--> **************************************************************************
-->  eq tosca-lemma05(SetCP,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDCP)
-->     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
-->		SetCP,STCP,(req(connectsTo,IDRQ,SRQ1,IDND) SetRQ),STRQ,
-->		SetRL,(opMsg(IDCP) MP)) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
-->		SetCP,STCP,(req(connectsTo,IDRQ,SRQ2,IDND) SetRQ),STRQ,
-->		SetRL,MP) 
-->     when (SRQ2 in STRQ) and (IDRQ = req(getRLOfCP(SetRL, IDCP))) .
--> **************************************************************************
select TOSCALEMMA .
:goal {
  eq tosca-lemma05(empCP,stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma05(sCP,stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP) implies
      tosca-lemma05((cap(trl,idCP',scp,idND') sCP),stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP)) = true .
}
:def ctftrl = :ctf {
  eq trl = connectsTo .
}
:def ctfidCP' = :ctf {
  eq idCP' = idCP .
}
:apply (ctfidCP' ctftrl rd-)
:def ctfscp = :ctf {
  eq (scp in stCP) = true .
}
:apply (ctfscp rd-)
:def ctfidRQ = :ctf {
  eq req(getRLOfCP(sRL,idCP)) = idRQ .
}
:apply (ctfidRQ rd-)
:def ctfidRQ' = :ctf {
  eq req(getRLOfCP(sRL,idCP')) = idRQ .
}
:apply (ctfidRQ' rd-)
show proof
select

--> Proof of tosca-lemma06.
--> *****************************************************************************
-->  eq tosca-lemma06(SetCP,TRL,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDRL)
-->     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
-->		SetCP,STCP,(req(TRL,IDRQ,SRQ1,IDND) SetRQ),STRQ,
-->		SetRL,MP) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
-->		SetCP,STCP,(req(TRL,IDRQ,SRQ2,IDND) SetRQ),STRQ,
-->		SetRL,MP)
-->     when not (TRL = connectsTo) and 
-->          uniqCP(SetCP) and
-->          allCPHaveRL(SetCP,SetRL) and
-->          allRLHaveRQ(SetRL,SetRQ) and
-->          allRLHaveSameTypeCPRQ(SetRL,SetCP,(req(TRL,IDRQ,SRQ2,IDND) SetRQ)) .
--> *****************************************************************************
select TOSCALEMMA .
:goal {
  eq tosca-lemma06(empCP,
		   trl,stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idRL) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma06(sCP,
		    trl,stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idRL) implies
      tosca-lemma06((cap(trl',idCP,scp,idND') sCP),
		    trl,stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idRL)) = true .
}
:init as tosca-lemma02-in [tosca-lemma02] .
:def ctftrl = :ctf {
  eq trl = connectsTo .
}
-- Since :apply(xxx rd-) is too time consuming, we use :apply(xxx):appy(rd) instead.
-- :apply (ctftrl rd-)
:apply (ctftrl)
:apply (rd)
:def ctfsCPa = :ctf {
  eq uniqCP(sCP) = true .
}
:def ctfidCPa = :ctf {
  eq existCP(sCP,idCP) = true .
}
:apply (ctfsCPa ctfidCPa rd-)
:def ctfsCPb = :ctf {
  eq allCPHaveRL(sCP,sRL) = true .
}
:def ctfidCPb = :ctf {
  eq onlyOneRLOfCP(sRL,idCP) = true .
}
:apply (ctfsCPb ctfidCPb rd-)
:def ctfsRLa = :ctf {
  eq allRLHaveRQ(sRL,sRQ) = true .
}
:apply (ctfsRLa rd-)
:def ctftrl' = :ctf {
  eq trl' = connectsTo .
}
:apply (ctftrl' rd-)
:def cspidCP = :csp {
  eq existCP(sRL,idCP) = false .
  eq sRL = (rel(trl',idRL,idCP,idRQ') sRL') .
}
-- Since :apply(xxx rd-) is too time consuming, we use :apply(xxx):appy(rd) instead.
-- :apply (cspidCP rd-)
:apply (cspidCP)
:apply (rd)
:def ctftrl'' = :ctf {
  eq trl'' = connectsTo .
}
:def ctfidRQ' = :ctf {
  eq idRQ' = idRQ .
}
:apply (ctftrl'' ctfidRQ' rd-)
show proof
select

--> Proof of tosca-lemma07.
--> **************************************************************************
-->  eq tosca-lemma07(SetCP,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDCP)
-->     = ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
-->		SetCP,STCP,(req(connectsTo,IDRQ,SRQ1,IDND) SetRQ),STRQ,
-->		SetRL,(avMsg(IDCP) MP)) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(
-->		SetCP,STCP,(req(connectsTo,IDRQ,SRQ2,IDND) SetRQ),STRQ,
-->		SetRL,MP) 
-->     when SRQ2 in STRQ .
--> **************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma07(empCP,scp,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma07(sCP,scp,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP) implies
      tosca-lemma07((cap(trl,idCP',scp',idND') sCP),scp,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP)) = true .
}  
:def ctftrl = :ctf {
  eq trl = connectsTo .
}
:apply (ctftrl rd-)
:def ctfsrq' = :ctf {
  eq (srq' in stRQ) = true .
}
:apply (ctfsrq' rd-)
:def ctfidCP' = :ctf {
  eq req(getRLOfCP(sRL,idCP')) = idRQ .
}
:apply (ctfidCP' rd-)
show proof
select

--> Proof of tosca-lemma08.
--> *******************************************************************************************
-->  eq tosca-lemma08(SetCP,SCP,STCP,STRQ,SetRQ,SetRL,MP)
-->     = allCPInStates(SetCP,SCP) 
-->       implies ifConnectsToCPInStatesThenRQInStatesOrOpenMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP)
-->     when not (SCP in STCP) .
--> *******************************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma08(empCP,scp,stCP,stRQ,sRQ,sRL,mp) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma08(sCP,scp,stCP,stRQ,sRQ,sRL,mp) implies
      tosca-lemma08((cap(trl,idCP,scp',idND') sCP),scp,stCP,stRQ,sRQ,sRL,mp)) = true .
}  
:def ctftrl = :ctf {
  eq trl = connectsTo .
}
:apply (ctftrl rd-)
:def ctfscp = :ctf {
  eq (scp in stCP) = true .
}
:apply (ctfscp rd-)
:def ctfscp' = :ctf {
  eq scp' = scp .
}
:apply (ctfscp' rd-)
show proof
select

--> Proof of tosca-lemma09.
--> ********************************************************************************************************
-->  eq tosca-lemma09(SetCP,STCP,SetRQ,STRQ,SetRL,MP,IDCP)
-->     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,(opMsg(IDCP) MP)) .
--> ********************************************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma09(empCP,scp,sRQ,srq,sRL,mp,idCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma09(sCP,scp,sRQ,srq,sRL,mp,idCP) implies
      tosca-lemma09((cap(trl,idCP',scp,idND') sCP),scp,sRQ,srq,sRL,mp,idCP)) = true .
}  
:def ctftrl = :ctf {
  eq trl = connectsTo .
}
:apply (ctftrl rd-)
show proof
select

--> Proof of tosca-lemma10.
--> ********************************************************************************************************
-->  eq tosca-lemma10(SetCP,STCP,SetRQ,STRQ,SetRL,MP,IDCP)
-->     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,(avMsg(IDCP) MP)) .
--> ********************************************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma10(empCP,scp,sRQ,srq,sRL,mp,idCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma10(sCP,scp,sRQ,srq,sRL,mp,idCP) implies
      tosca-lemma10((cap(trl,idCP',scp,idND') sCP),scp,sRQ,srq,sRL,mp,idCP)) = true .
}  
:def ctfidCP' = :ctf {
  eq idCP' = idCP .
}  
:def ctftrl = :ctf {
  eq trl = connectsTo .
}
:apply (ctfidCP' ctftrl rd-)
show proof
select

--> Proof of tosca-lemma11.
--> **************************************************************************
-->  eq tosca-lemma11(SetCP,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDCP)
-->     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
-->		SetCP,STCP,(req(connectsTo,IDRQ,SRQ1,IDND) SetRQ),STRQ,
-->		SetRL,(opMsg(IDCP) MP)) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
-->		SetCP,STCP,(req(connectsTo,IDRQ,SRQ2,IDND) SetRQ),STRQ,
-->		SetRL,MP) 
-->     when not (SRQ1 in STRQ) .
--> **************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma11(empCP,scp,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma11(sCP,scp,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP) implies
      tosca-lemma11((cap(trl,idCP',scp,idND') sCP),scp,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP)) = true .
}  
:def ctftrl = :ctf {
  eq trl = connectsTo .
}
:apply (ctftrl rd-)
:def ctfidRQ = :ctf {
  eq req(getRLOfCP(sRL,idCP')) = idRQ .
}
:apply (ctfidRQ rd-)
show proof
select

--> Proof of tosca-lemma12.
--> *****************************************************************************
-->  eq tosca-lemma12(SetCP,TRL,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND)
-->     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
-->		SetCP,STCP,(req(TRL,IDRQ,SRQ1,IDND) SetRQ),STRQ,SetRL,MP) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
-->		SetCP,STCP,(req(TRL,IDRQ,SRQ2,IDND) SetRQ),STRQ,SetRL,MP)
-->     when not (TRL = connectsTo) and 
-->          uniqCP(SetCP) and
-->          allCPHaveRL(SetCP,SetRL) and
-->          allRLHaveRQ(SetRL,SetRQ) and
-->          allRLHaveSameTypeCPRQ(SetRL,SetCP,(req(TRL,IDRQ,SRQ2,IDND) SetRQ)) .
--> *****************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma12(empCP,trl,stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma12(sCP,trl,stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND) implies
      tosca-lemma12((cap(trl',idCP,scp,idND') sCP),trl,stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND)) = true .
}  
:init as tosca-lemma02-in [tosca-lemma02] .
:def ctftrl = :ctf {
  eq trl = connectsTo .
}
-- Since :apply(xxx rd-) is too time consuming, we use :apply(xxx):appy(rd) instead.
-- :apply (ctftrl rd-)
:apply (ctftrl)
:apply (rd)
:def ctfsCPa = :ctf {
  eq uniqCP(sCP) = true .
}
:def ctfidCPa = :ctf {
  eq existCP(sCP,idCP) = true .
}
:apply (ctfsCPa ctfidCPa rd-)
:def ctfsCPb = :ctf {
  eq allCPHaveRL(sCP,sRL) = true .
}
:def ctfidCPb = :ctf {
  eq onlyOneRLOfCP(sRL,idCP) = true .
}
:apply (ctfsCPb ctfidCPb rd-)
:def ctfsRLa = :ctf {
  eq allRLHaveRQ(sRL,sRQ) = true .
}
:apply (ctfsRLa rd-)
:def ctftrl' = :ctf {
  eq trl' = connectsTo .
}
:apply (ctftrl' rd-)
:def cspidCP = :csp {
  eq existCP(sRL,idCP) = false .
  eq sRL = (rel(trl',idRL,idCP,idRQ') sRL') .
}
-- Since :apply(xxx rd-) is too time consuming, we use :apply(xxx):appy(rd) instead.
-- :apply (cspidCP rd-)
:apply (cspidCP)
:apply (rd)
:def ctftrl'' = :ctf {
  eq trl'' = connectsTo .
}
:def ctfidRQ' = :ctf {
  eq idRQ' = idRQ .
}
:apply (ctftrl'' ctfidRQ' rd-)
show proof
select

--> Proof of tosca-lemma13.
--> ************************************************************************************************
-->  eq tosca-lemma13(SetCP,SCP,STCP,STRQ,SetRQ,SetRL,MP)
-->     = allCPInStates(SetCP,SCP) 
-->       implies ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(SetCP,STCP,SetRQ,STRQ,SetRL,MP)
-->     when not (SCP in STCP) .
--> ************************************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma13(empCP,scp,stCP,stRQ,sRQ,sRL,mp) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma13(sCP,scp,stCP,stRQ,sRQ,sRL,mp) implies
      tosca-lemma13((cap(trl,idCP,scp,idND) sCP),scp,stCP,stRQ,sRQ,sRL,mp)) = true .
}  
:def ctfscp = :ctf {
  eq (scp in stCP) = true .
}
:apply (ctfscp rd-)
show proof
select

--> Proof of tosca-lemma14.
--> **************************************************************************
-->  eq tosca-lemma14(SetCP,STCP,SetRQ,SRQ1,SRQ2,STRQ,SetRL,MP,IDRQ,IDND,IDCP)
-->     = ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
-->		SetCP,STCP,(req(connectsTo,IDRQ,SRQ1,IDND) SetRQ),STRQ,
-->		SetRL,(avMsg(IDCP) MP)) implies
-->       ifConnectsToCPInStatesThenRQInStatesOrAvailableMsg(
-->		SetCP,STCP,(req(connectsTo,IDRQ,SRQ2,IDND) SetRQ),STRQ,
-->		SetRL,MP) 
-->     when (SRQ2 in STRQ) and (IDRQ = req(getRLOfCP(SetRL, IDCP))) .
--> **************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma14(empCP,
		   stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma14(sCP,
		    stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP) implies
      tosca-lemma14((cap(trl,idCP',scp,idND) sCP),
		    stCP,sRQ,srq,srq',stRQ,sRL,mp,idRQ,idND,idCP)) = true .
}  
:def ctftrl = :ctf {
  eq trl = connectsTo .
}
:def ctfidCP' = :ctf {
  eq idCP' = idCP .
}
:apply (ctfidCP' ctftrl rd-)
:def ctfscp = :ctf {
  eq (scp in stCP) = true .
}
:apply (ctfscp rd-)
:def ctfidRQ = :ctf {
  eq req(getRLOfCP(sRL,idCP)) = idRQ .
}
:apply (ctfidRQ rd-)
:def ctfidRQ' = :ctf {
  eq req(getRLOfCP(sRL,idCP')) = idRQ .
}
:apply (ctfidRQ' rd-)
show proof
select

--> Proof of tosca-lemma15.
--> ******************************************************************************
-->  eq tosca-lemma15(MP,SetCP,IDCP,IDND,TRL,SCP1,SCP2,STCP)
-->     = ifOpenMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP1,IDND) SetCP),STCP) implies
-->       ifOpenMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP2,IDND) SetCP),STCP) 
-->     when (SCP2 in STCP) .
--> ******************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma15(empMsg,sCP,idCP,idND,trl,scp,scp',stCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma15(mp,sCP,idCP,idND,trl,scp,scp',stCP) implies
      tosca-lemma15((msg mp),sCP,idCP,idND,trl,scp,scp',stCP)) = true .
}
:def cspmsg = :csp {
  eq msg = opMsg(idCP') .
  eq msg = avMsg(idCP') .
}
:def ctfidCP' = :ctf {
  eq idCP' = idCP .
}
:apply (ctfidCP' cspmsg rd-)
show proof
select

--> Proof of tosca-lemma16.
--> ******************************************************************************
-->  eq tosca-lemma16(MP,SetCP,IDCP,IDND,TRL,SCP1,SCP2,STCP)
-->     = ifOpenMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP1,IDND) SetCP),STCP) implies
-->       ifOpenMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP2,IDND) SetCP),STCP) 
-->     when not (SCP1 in STCP) .
--> ******************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma16(empMsg,sCP,idCP,idND,trl,scp,scp',stCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma16(mp,sCP,idCP,idND,trl,scp,scp',stCP) implies
      tosca-lemma16((msg mp),sCP,idCP,idND,trl,scp,scp',stCP)) = true .
}
:def cspmsg = :csp {
  eq msg = opMsg(idCP') .
  eq msg = avMsg(idCP') .
}
:def ctfidCP' = :ctf {
  eq idCP' = idCP .
}
:apply (ctfidCP' cspmsg rd-)
show proof
select

--> Proof of tosca-lemma17.
--> ***********************************************************************************
-->  eq tosca-lemma17(MP,SetCP,IDCP,IDND,TRL,SCP1,SCP2,STCP)
-->     = ifAvailableMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP1,IDND) SetCP),STCP) implies
-->       ifAvailableMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP2,IDND) SetCP),STCP)
-->     when (SCP2 in STCP) .
--> ***********************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma17(empMsg,sCP,idCP,idND,trl,scp,scp',stCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma17(mp,sCP,idCP,idND,trl,scp,scp',stCP) implies
      tosca-lemma17((msg mp),sCP,idCP,idND,trl,scp,scp',stCP)) = true .
}
:def cspmsg = :csp {
  eq msg = opMsg(idCP') .
  eq msg = avMsg(idCP') .
}
:def ctfidCP' = :ctf {
  eq idCP' = idCP .
}
:apply (cspmsg ctfidCP' rd-)
show proof
select

--> Proof of tosca-lemma18.
--> ***********************************************************************************
-->  eq tosca-lemma18(MP,SetCP,IDCP,IDND,TRL,SCP1,SCP2,STCP)
-->     = ifAvailableMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP1,IDND) SetCP),STCP) implies
-->       ifAvailableMsgThenCPInStates(MP,(cap(TRL,IDCP,SCP2,IDND) SetCP),STCP)
-->     when not (SCP1 in STCP) .
--> ***********************************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma18(empMsg,sCP,idCP,idND,trl,scp,scp',stCP) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma18(mp,sCP,idCP,idND,trl,scp,scp',stCP) implies
      tosca-lemma18((msg mp),sCP,idCP,idND,trl,scp,scp',stCP) ) = true .
}
:def cspmsg = :csp {
  eq msg = opMsg(idCP') .
  eq msg = avMsg(idCP') .
}
:def ctfidCP' = :ctf {
  eq idCP' = idCP .
}
:apply (cspmsg ctfidCP' rd-)
show proof
select

--> Proof of tosca-lemma19.
--> *********************************************************************
-->  eq tosca-lemma19(SetCP,SRQ,STRQ,STCP,SetRQ,SetRL)
-->     = (allRQInStates(SetRQ,SRQ) and 
-->        allCPHaveRL(SetCP,SetRL) and allRLHaveRQ(SetRL,SetRQ)) implies
-->       ifCPInStatesThenRQInStates(SetCP,STCP,SetRQ,STRQ,SetRL) 
-->     when SRQ in STRQ .
--> *********************************************************************
select TOSCALEMMA
:goal {
  eq tosca-lemma19(empCP,srq,stRQ,stCP,sRQ,sRL) = true .
}
:apply (rd-)
show proof
:goal {
  eq (tosca-lemma19(sCP,srq,stRQ,stCP,sRQ,sRL) implies
      tosca-lemma19((cap(trl,idCP,scp,idND) sCP),srq,stRQ,stCP,sRQ,sRL)) = true .
}
:def ctfsrq = :ctf {
  eq (srq in stRQ) = true .
}
:apply (ctfsrq rd-)
:def cspidCP = :csp {
  eq onlyOneRLOfCP(sRL,idCP) = false .
  eq sRL = (rel(trl,idRL,idCP,idRQ) sRL') .
}
:apply (cspidCP rd-)
:def cspidRQ = :csp {
  eq existRQ(sRQ,idRQ) = false .
  eq sRQ = (req(trl,idRQ,srq',idND') sRQ') .
}
:apply (cspidRQ rd-)
:def ctfsrq' = :ctf {
  eq srq' = srq .
}  
:apply (ctfsrq' rd-)
show proof
select
--> Totally 36 goals should be proved.
