require ProofContCont Proof-contcont.cafe

-- As to rule R01, we have checked all possible cases, however,
-- here we will omit irrevant cases in which wfs(S) does not hold and thus contcont(S) holds.

--> *****************************************************************************************
--> ctrans [R10]:
-->    < SetND, (cap(connectsTo,IDCP,open,     IDND) SetCP), SetRQ, SetRL, MP >
--> => < SetND, (cap(connectsTo,IDCP,available,IDND) SetCP), SetRQ, SetRL, (avMsg(IDCP) MP) >
-->    if state(getNode(SetND,IDND)) = started .
--> *****************************************************************************************
--> Rule R10 sends an available message, so next applied rules may be Rule R12.
--> Case 1: The condition of R10 does not hold in S, otherwise
--> Case 2-14:
-->  #     R1        N1
-->  2-10  unbound   initial -> Cyclic Dependency Theorem ensures that contcont holds.
-->  11/12 unbound   created -> R11 is applicable
-->  13    unbound   started -> not reachable
-->  14    waiting   *       -> R12 is applicable
-->  15    ready     *       -> not reachable

--> Case 1:
--> (1-1) The condition of R10 does not hold.
--> In this case, there is no applicable rule.
open ProofContCont .
  eq (state(getNode(sND,idND)) = started) = false .
  red contcont(< sND,(cap(connectsTo,idCP,open,idND) sCP),sRQ,sRL,mp >,
               SS:State,CC:Bool) .
close
--> Case 2-10:
--> (1-2) The condition of R10 holds, which means that
-->       the node of the capability is started,
--> (2-1) The corresponding requirement is unbound,
--> (3-1) The corresponding node is initial.
-->       Cyclic Dependency Theorem ensures that
-->       there is a node N1 which is initial and DDSR01(N1,S) does not include initial nodes.
--> In this case, the proofs are same as Case 4-12 of initcont(S) .

--> Case 11:
--> (1-2) The condition of R10 holds, which means that
-->       the node of the capability is started,
--> (2-1) The corresponding requirement is unbound,
--> (3-2) The corresponding node is created,
--> (4-1) opMsg(idCP) is not in MP.
--> This case will be rejected by inv-ifConnectsToCPOpenThenRQWaitingOrOpenMsg(S).
open ProofContCont .
  eq inv(S:State) = inv-ifConnectsToCPOpenThenRQWaitingOrOpenMsg(S) .
  eq (opMsg(idCP) \in mp) = false .
  eq sND' = (node(tnd,idND1,created) sND'') .
  eq sRQ = (req(connectsTo,idRQ,unbound,idND1) sRQ') .
  eq sRL = (rel(connectsTo,idRL,idCP,idRQ) sRL') .
  eq sND = (node(tnd,idND,started) sND') .
  red contcont(< sND,(cap(connectsTo,idCP,open,idND) sCP),sRQ,sRL,mp >,
               SS:State,CC:Bool) .
close
--> Case 12:
--> (1-2) The condition of R10 holds, which means that
-->       the node of the capability is started,
--> (2-1) The corresponding requirement is unbound,
--> (3-2) The corresponding node is created,
--> (4-2) opMsg(idCP) is in MP.
--> In this case, Rule R11 is applicable in SS.
open ProofContCont .
  eq mp = (opMsg(idCP) mp') .
  eq sND' = (node(tnd,idND1,created) sND'') .
  eq sRQ = (req(connectsTo,idRQ,unbound,idND1) sRQ') .
  eq sRL = (rel(connectsTo,idRL,idCP,idRQ) sRL') .
  eq sND = (node(tnd,idND,started) sND') .
  red contcont(< sND,(cap(connectsTo,idCP,open,idND) sCP),sRQ,sRL,mp >,
               SS:State,CC:Bool) .
close
--> Case 13:
--> (1-2) The condition of R10 holds, which means that
-->       the node of the capability is started,
--> (2-1) The corresponding requirement is unbound,
--> (3-3) The corresponding node is started.
--> This case will be rejected by inv-ifNDStartedThenRQReady(S) .
open ProofContCont .
  eq inv(S:State) = inv-ifNDStartedThenRQReady(S) .
  eq sND' = (node(tnd,idND1,started) sND'') .
  eq sRQ = (req(connectsTo,idRQ,unbound,idND1) sRQ') .
  eq sRL = (rel(connectsTo,idRL,idCP,idRQ) sRL') .
  eq sND = (node(tnd,idND,started) sND') .
  red contcont(< sND,(cap(connectsTo,idCP,open,idND) sCP),sRQ,sRL,mp >,
               SS:State,CC:Bool) .
close
--> Case 14:
--> (1-2) The condition of R10 holds, which means that
-->       the node of the capability is started,
--> (2-2) The corresponding requirement is waiting.
--> In this case, Rule R12 is applicable in SS.
open ProofContCont .
  eq sRQ = (req(connectsTo,idRQ,waiting,idND1) sRQ') .
  eq sRL = (rel(connectsTo,idRL,idCP,idRQ) sRL') .
  eq sND = (node(tnd,idND,started) sND') .
  red contcont(< sND,(cap(connectsTo,idCP,open,idND) sCP),sRQ,sRL,mp >,
               SS:State,CC:Bool) .
close
--> Case 15:
--> (1-2) The condition of R10 holds, which means that
-->       the node of the capability is started,
--> (2-3) The corresponding requirement is ready.
--> This case will be rejected by inv-ifCPOpenThenRQUnboundWaiting(S).
open ProofContCont .
  eq inv(S:State) = inv-ifCPOpenThenRQUnboundWaiting(S) .
  eq sRQ = (req(connectsTo,idRQ,ready,idND1) sRQ') .
  eq sRL = (rel(connectsTo,idRL,idCP,idRQ) sRL') .
  eq sND = (node(tnd,idND,started) sND') .
  red contcont(< sND,(cap(connectsTo,idCP,open,idND) sCP),sRQ,sRL,mp >,
               SS:State,CC:Bool) .
close
