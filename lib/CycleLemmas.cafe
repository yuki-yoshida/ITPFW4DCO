require Template Template.cafe

provide Lemmas

module CYCLELEMMA {
  protecting(CYCLEPRED)
  
  pred someNoDependObjDS : SetOfObject LocalState State
  pred noCycleLemma : Object State LocalState
  pred cycle-lemma1 : Object LocalState State
  pred cycle-lemma2 : SetOfObject LocalState State

  vars O O' : Object
  vars OS OS' OS0 OS0' : SetOfObject
  var LS : LocalState
  var S : State

  -- Assuming subset(DDSC(O,S),getAllObjC(S)) .
  eq [DDSCinS :nonexec]:
    subset(DDSC(O,S),getAllObjC(S)) = true .

  eq someNoDependObjDS(empObj,LS,S) = false . 
  ceq someNoDependObjDS((O OS),LS,S) 
    = isNoDepend(O,LS,S) or someNoDependObjDS(DDSC(O,S),LS,S)
    if O in LS .
  ceq someNoDependObjDS((O OS),LS,S) 
    = someNoDependObjDS(OS,LS,S)
    if not (O in LS) .

  -- Lemmas to be proved
  eq noCycleLemma(O,S,LS)
    = (noCycleC(S) and O in getAllObjC(S) and O in LS) 
      implies someNoDependObj(getAllObjC(S), LS, S) .

  eq cycle-lemma1(O,LS,S)
    = (noCycleC(S) and 
       O in getAllObjC(S) and 
       O in LS) implies 
      someNoDependObjDS(O,LS,S) .

  eq cycle-lemma2(OS,LS,S)
    = subset(OS,getAllObjC(S)) and someNoDependObjDS(OS,LS,S) 
     implies someNoDependObj(getAllObjC(S), LS, S) .

  ops o o' : -> Object .
  ops os os' : -> SetOfObject .
  op ls : -> LocalState .
  op s : -> State .
}

--> Proof of cycle-lemma1
-->  eq cycle-lemma1(O,LS,S)
-->    = (noCycleC(S) and 
-->       O in getAllObjC(S) and 
-->       O in LS) implies 
-->      someNoDependObjDS(O,LS,S) .
open CYCLELEMMA .

  -- Set Axiom
  ceq [OinOS :nonexec]:
      (O in OS) = true
   if subset((O OS'),OS) .

  [OLSS] op t : Object LocalState State -> OLSS {constr} .
  pred _<wf_ : OLSS OLSS .

  -- Induction Hypothesis
  ceq [cycle-lemma1-IH :nonexec]:
    cycle-lemma1(O,LS,S) = true
    if t(O,LS,S) <wf t(o,ls,s) .

  -- Define <wf for cycle-lemma1
  ceq ( t(O',LS,S) <wf t(O,LS,S) ) = true
     if O' in DDSC(O,S) .

:goal { eq cycle-lemma1(o,ls,s) = true . }
:def OinScsp = :csp {
  eq (o in getAllObjC(s)) = false .
  eq getAllObjC(s) = (o os) .
}
:apply (OinScsp rd-)
:def OinLSctf = :ctf {
  eq (o in ls) = true .
}
:apply (OinLSctf rd-)
:def allNScsp = :csp {
  eq allObjNotInState(DDSC(o,s),ls) = true .
  eq DDSC(o,s) = (o' os') .
}
:apply (allNScsp rd-)
:csp {
  eq (o' in ls) = true .
}
:def OisO'ctf = :ctf {
  eq (o' in o) = true .
}
:apply (OisO'ctf rd-)
:init as DDSCinS-in [DDSCinS]
  by {O:Object <- o;
      S:State <- s; }
:init as OinOS-in [OinOS]
  by {O:Object <- o';
      OS:SetOfObject <- getAllObjC(s);
      OS':SetOfObject <- os'; }
:init as cycle-lemma1-IH-in [cycle-lemma1-IH]
  by {O:Object <- o'; 
      LS:LocalState <- ls; 
      S:State <- s; }
:apply (rd-)
--> Proof of cycle-lemma1 done
-->
show proof
close

--> Proof of cycle-lemma2
-->  eq cycle-lemma2(OS,LS,S)
-->    = subset(OS,getAllObjC(S)) and someNoDependObjDS(OS,LS,S) 
-->     implies someNoDependObj(getAllObjC(S), LS, S) .
open CYCLELEMMA .

  [OSLSS] op t : SetOfObject LocalState State -> OSLSS {constr} .
  pred _<wf_ : OSLSS OSLSS .

  -- Induction Hypothesis
  ceq [cycle-lemma2-IH :nonexec]:
     cycle-lemma2(OS,LS,S) = true
     if t(OS,LS,S) <wf t(os,ls,s) .

  -- Define <wf for cycle-lemma2
  eq ( t(DDSC(O,S),LS,S) <wf t((O OS),LS,S) ) = true .
  ceq ( t(OS',LS,S) <wf t(OS,LS,S) ) = true
    if OS' <wf OS .

:goal { eq cycle-lemma2(os,ls,s) = true . }
:def empOScsp = :csp {
  eq os = empObj .
  eq os = (o os') .
}
:apply (empOScsp rd-)
:def OSinScsp = :csp {
  eq subset((o os'),getAllObjC(s)) = false .
  eq getAllObjC(s) = (o os' os) .
}
:apply (OSinScsp rd-)
:def OinLSctf = :ctf {
  eq (o in ls) = true .
}
:apply (OinLSctf rd-)
:def allNSctf = :ctf {
  eq allObjNotInState(DDSC(o,s),ls) = true .
}
:apply (allNSctf rd-)
-- Case 2-2-1-2: eq allObjNotInState(DDSC(o, s), ls)  = false .
:init as DDSCinS-in [DDSCinS]
  by {O:Object <- o;
      S:State <- s; }
:apply(rd-)
:init as cycle-lemma2-IH-in [cycle-lemma2-IH]
  by {OS:SetOfObject <- DDSC(o,s); 
      LS:LocalState <- ls; 
      S:State <- s; }
:apply (rd-)
-- Case 2-2-2: o in ls = false
:init as cycle-lemma2-IH-in [cycle-lemma2-IH]
  by {OS:SetOfObject <- os'; 
      LS:LocalState <- ls; 
      S:State <- s; }
:apply (rd-)
--> Proof of cycle-lemma2 done
-->
show proof
close

--> Proof of noCycleLemma
-->  eq noCycleLemma(O,S,LS)
-->    = (noCycleC(S) and O in getAllObjC(S) and O in LS)
-->      implies someNoDependObj(getAllObjC(S), LS, S) .
open CYCLELEMMA .

  -- sublemma
  -- eq cycle-lemma1(O,LS,S)
  --   = (noCycleC(S) and 
  --      O in getAllObjC(S) and 
  --      O in LS) implies 
  --     someNoDependObjDS(O,LS,S) .
  ceq someNoDependObjDS(O,LS,S) = true
   if (noCycleC(S) and 
       O in getAllObjC(S) and 
       O in LS) .

  -- eq cycle-lemma2(OS,LS,S)
  --   = subset(OS,getAllObjC(S)) and someNoDependObjDS(OS,LS,S) 
  --    implies someNoDependObj(getAllObjC(S), LS, S) .
  ceq [cycle-lemma2 :nonexec]:
      someNoDependObj(getAllObjC(S), LS, S) = true
   if subset(OS,getAllObjC(S)) and someNoDependObjDS(OS,LS,S) .

:goal { eq noCycleLemma(o,s,ls) = true . }
:def noCyclectf = :ctf {
  eq noCycleC(s) = true .
}
:apply (noCyclectf rd-)
:def OinSctf = :ctf {
  eq (o in getAllObjC(s)) = true .
}
:apply (OinSctf rd-)
:def OinLSctf = :ctf {
  eq (o in ls) = true .
}
:apply (OinLSctf rd-)
:init as cycle-lemma2-in [cycle-lemma2]
  by {OS:SetOfObject <- o;
      LS:LocalState <- ls;
      S:State <- s; }
:apply (rd-)
--> Proof of noCycleLemma done
-->
show proof
close 

--> Totally 3 goals should be proved
