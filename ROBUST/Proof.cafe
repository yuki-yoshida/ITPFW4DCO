require DomainModel DomainModel.cafe
provide Proof

module ProofBase {
  protecting(STATERules)

  op _!_ : Bool Bool -> Bool {constr assoc}
  op _=>_!!_ : State State Bool -> Bool {constr}

  var CO : Component
  var SCO : SetOfComponent
  vars IDCO IIDCO EIDCO : COID 
  vars CSCO NSCO TSCO : COState
  var StCO : SetOfCOState

  var IM : Import
  var SIM : SetOfImport
  var IDIM : IMID
  var TIM : IMType
  vars CSIM NSIM TSIM : IMState
  var StIM : SetOfIMState

  var L : ListOfLog
  vars S SS : State

  -- The invariant should be defined as the conjunction of all inv-* and all wfs-*.
  -- But this definition causes costly computations.

--  pred inv-ifCOStartedThenIMReady : State
--  eq inv-ifCOStartedThenIMReady(< SCO,SIM,L >)
--     = ifCOInStatesThenIMInStates(SCO,started,SIM,ready) .

--> *************************
--> Step 0-2: Define cont(S).
--> *************************
  pred cont : State
  eq cont(S)
     = (S =(*,1)=>+ SS) .

--> **********************
--> Step 0-3: Define m(S).
--> **********************
  -- Measuring functions
  -- Target Distance
  op tdis : SetOfComponent SetOfComponent SetOfImport -> Nat
  op tdis : SetOfImport SetOfComponent SetOfImport -> Nat

  -- Forced Distance
  -- Forced to become stopped temporally
  op fdis : Component SetOfComponent SetOfImport -> Nat
  -- Forced to become unwired temporally
  op fdis : SetOfImport SetOfComponent SetOfImport -> Nat

  var SCO0 : SetOfComponent
  var SIM0 : SetOfImport

  eq tdis(empCO,SCO0,SIM0) = 0 .
  eq tdis((CO SCO),SCO0,SIM0) = tdis(CO,SCO0,SIM0) + tdis(SCO,SCO0,SIM0) .
  eq tdis(empIM,SCO0,SIM0) = 0 .
  eq tdis((IM SIM),SCO0,SIM0) = tdis(IM,SCO0,SIM0) + tdis(SIM,SCO0,SIM0) .
  eq fdis(empIM,SCO0,SIM0) = 0 .
  eq fdis((IM SIM),SCO0,SIM0) = fdis(IM,SCO0,SIM0) + fdis(SIM,SCO0,SIM0) .

  eq tdis(cmp(IDCO,started,started,stopped),SCO0,SIM0)
    = 2 + tdis(getIMsOfECO(SIM0,IDCO),SCO0,SIM0) .
  eq tdis(cmp(IDCO,started,stopped,stopped),SCO0,SIM0)
    = 1  + tdis(getIMsOfECO(SIM0,IDCO),SCO0,SIM0) .
  eq tdis(cmp(IDCO,stopped,stopped,stopped),SCO0,SIM0)
    = 0  + tdis(getIMsOfECO(SIM0,IDCO),SCO0,SIM0) .
  eq tdis(cmp(IDCO,stopped,started,stopped),SCO0,SIM0) = 100 . -- do not happen

  eq tdis(cmp(IDCO,stopped,stopped,started),SCO0,SIM0)
    = 2  + tdis(getIMsOfECO(SIM0,IDCO),SCO0,SIM0) .
  eq tdis(cmp(IDCO,stopped,started,started),SCO0,SIM0)
    = 1  + tdis(getIMsOfECO(SIM0,IDCO),SCO0,SIM0) .
  eq tdis(cmp(IDCO,started,started,started),SCO0,SIM0)
    = 0  + tdis(getIMsOfECO(SIM0,IDCO),SCO0,SIM0) .
  eq tdis(cmp(IDCO,started,stopped,started),SCO0,SIM0)
    = 0  + tdis(getIMsOfECO(SIM0,IDCO),SCO0,SIM0) .

  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,  wired,  wired,unwired),SCO0,SIM0) = 2 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,  wired,unwired,unwired),SCO0,SIM0) = 1 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,unwired,unwired,unwired),SCO0,SIM0) = 0 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,unwired,  wired,unwired),SCO0,SIM0) = 100 .

  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,  wired, unused,unwired),SCO0,SIM0) = 100 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,unwired, unused,unwired),SCO0,SIM0) = 100 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO, unused, unused,unwired),SCO0,SIM0) = 100 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO, unused,unwired,unwired),SCO0,SIM0) = 100 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO, unused,  wired,unwired),SCO0,SIM0) = 100 .

  eq tdis(imp(TIM,IDIM,IIDCO,IDCO, unused, unused,  wired),SCO0,SIM0) = 4 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO, unused,unwired,  wired),SCO0,SIM0) = 3 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO, unused,  wired,  wired),SCO0,SIM0) = 100 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,unwired, unused,  wired),SCO0,SIM0) = 100 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,  wired, unused,  wired),SCO0,SIM0) = 100 .

  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,unwired,unwired,  wired),SCO0,SIM0) = 2 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,unwired,  wired,  wired),SCO0,SIM0) = 1 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,  wired,  wired,  wired),SCO0,SIM0) = 0 .
  eq tdis(imp( optional,IDIM,IIDCO,IDCO,  wired,unwired,wired),SCO0,SIM0) = 0 .
  eq tdis(imp(mandatory,IDIM,IIDCO,IDCO,  wired,unwired,wired),SCO0,SIM0) = 0 .

  eq tdis(imp(optional,IDIM,IIDCO,IDCO,  wired,  wired,unused),SCO0,SIM0) = 4 .
  eq tdis(imp(optional,IDIM,IIDCO,IDCO,  wired,unwired,unused),SCO0,SIM0) = 3 .
  eq tdis(imp(mandatory,IDIM,IIDCO,IDCO,  wired,  wired,unused),SCO0,SIM0) 
    = 4 + fdis(getComponent(SCO0,IIDCO),SCO0,SIM0) .  -- case (A)
  eq tdis(imp(mandatory,IDIM,IIDCO,IDCO,  wired,unwired,unused),SCO0,SIM0)
    = 3 + fdis(getComponent(SCO0,IIDCO),SCO0,SIM0) .  -- case (A)
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,  wired, unused,unused),SCO0,SIM0) = 100 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO, unused,  wired,unused),SCO0,SIM0) = 100 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,unwired,  wired,unused),SCO0,SIM0) = 100 .

  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,unwired,unwired,unused),SCO0,SIM0) = 2 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO,unwired, unused,unused),SCO0,SIM0) = 1 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO, unused, unused,unused),SCO0,SIM0) = 0 .
  eq tdis(imp(TIM,IDIM,IIDCO,IDCO, unused,unwired,unused),SCO0,SIM0) = 100 .

  ceq fdis(cmp(IDCO,CSCO,NSCO,TSCO),SCO0,SIM0) 
     = 0 if not (CSCO == started) or not (TSCO == started) .
  eq fdis(cmp(IDCO,started,started,started),SCO0,SIM0) 
     = 4 + fdis(getIMsOfECOInTargetStates(SIM0,IDCO,wired),SCO0,SIM0) . -- case (C)
  eq fdis(cmp(IDCO,started,stopped,started),SCO0,SIM0) 
     = 3 + fdis(getIMsOfECOInTargetStates(SIM0,IDCO,wired),SCO0,SIM0) . -- case (C)

  ceq fdis(imp(TIM,IDIM,IIDCO,IDCO,CSIM,NSIM,TSIM),SCO0,SIM0)
     = 0 if not (CSIM == wired) or not (TSIM in (wired unused)) .
  eq fdis(imp( optional,IDIM,IIDCO,IDCO,  wired,  wired,wired),SCO0,SIM0) = 4 .
  eq fdis(imp(mandatory,IDIM,IIDCO,IDCO,  wired,  wired,wired),SCO0,SIM0)
    = 4 + fdis(getComponent(SCO0,IIDCO),SCO0,SIM0) .  -- case (B)
  eq fdis(imp( optional,IDIM,IIDCO,IDCO,  wired,unwired,wired),SCO0,SIM0) = 3 .
  eq fdis(imp(mandatory,IDIM,IIDCO,IDCO,  wired,unwired,wired),SCO0,SIM0)
    = 3 + fdis(getComponent(SCO0,IIDCO),SCO0,SIM0) .  -- case (B)

  eq fdis(imp( optional,IDIM,IIDCO,IDCO,  wired,  wired,unused),SCO0,SIM0) = 4 .
  eq fdis(imp(mandatory,IDIM,IIDCO,IDCO,  wired,  wired,unused),SCO0,SIM0) 
    = 4 + fdis(getComponent(SCO0,IIDCO),SCO0,SIM0) .  -- case (A)
  eq fdis(imp( optional,IDIM,IIDCO,IDCO,  wired,unwired,unused),SCO0,SIM0) = 3 .
  eq fdis(imp(mandatory,IDIM,IIDCO,IDCO,  wired,unwired,unused),SCO0,SIM0)
    = 3 + fdis(getComponent(SCO0,IIDCO),SCO0,SIM0) .  -- case (A)

  eq m(< SCO,SIM,L >)
    = tdis(SCO, SCO, SIM) .
  eq mlog(SCO, SIM)
    = tdis(SCO, SCO, SIM) .

--> ************************
--> Step 0-4: Define inv(S).
--> ************************
  -- wfs-*:
  ceq wfs(S) = false if not wfs-uniqCO(S) .
  ceq wfs(S) = false if not wfs-uniqIM(S) .
  ceq wfs(S) = false if not wfs-allIMHaveICO(S) .
  ceq wfs(S) = false if not wfs-allIMHaveECO(S) .
  ceq wfs(S) = false if not wfs-validTarget(S) .
  ceq wfs(S) = false if not wfs-validCurrent(S) .
  ceq inv(S) = false if not wfs(S) .

  -- inv-*:
--  ceq inv(S) = false if not inv-ifCOStartedThenIMReady(S) .
--  ceq inv(S) = false if not noCOCycle(S) .

--> **************************************
--> Step 0-5: Prepare arbitrary constants.
--> **************************************
  ops idCO idCO' iidCO eidCO : -> COID
  ops idIM idIM' : -> IMID
  ops sCO sCO' : -> SetOfComponent
  ops sIM sIM' : -> SetOfImport
  ops tIM tIM' : -> IMType
  ops cCOs cCOs' nCOs nCOs' tCOs tCOs' : -> COState
  ops cIMs cIMs' nIMs nIMs' tIMs tIMs' : -> IMState
  op sCOs : -> SetOfCOState
  op sIMs : -> SetOfIMState
  op x : -> Component

  ops lg lg' : -> ListOfLog

  -- idCO1 is a COID different from idCO.
  op idCO1 : -> COID
  eq (idCO1 = idCO) = false .

  op cCOs1 : -> COState
  eq (cCOs = cCOs1) = false .

  op tCOs1 : -> COState
  eq (tCOs = tCOs1) = false .

}

-- *************************************************************************
-- (1) init(S) implies cont(S).
-- (2) inv(S) and not final(S) implies cont(SS) or final(SS).
-- (3) inv(S) and not final(S) implies measure(S) > measure(SS).
-- (4) init(S) implies inv(S).
-- (5) inv(S) implies inv(SS).
-- *************************************************************************

-- ****************************
-- (1) init(S) implies cont(S).
-- ****************************
-- in Proof-initcont.cafe

-- **********************************************************
-- (2) inv(S) and not final(S) implies cont(SS) or final(SS).
-- **********************************************************
-- in Proof-contcont.cafe

-- *************************************************************************
-- (3) inv(S) and not final(S) implies measure(S) > measure(SS).
-- *************************************************************************
-- in Proof-measure.cafe

-- ***************************
-- (4) init(S) implies inv(S).
-- (5) inv(S) implies inv(SS).
-- ***************************
-- in Proof-inv.cafe
