require Template ../lib/Template.cafe
require LIST ../lib/LIST.cafe
provide DomainModel

-- An abstract model for Robust Reconfiguration Algorithm by F. Boyer et al.

-- There are two kinds of objects:
-- Component(CO): a component of which a cloud system composes.
-- Import(IM): a directed relationship of two components
--  which means one of them (the importer) requires a service provided by other (exporter).

module! COMPONENT {
  -- Sorts
  [Component COID COType COState]
  -- Note: COType is requied by Template but is not used in this model

  -- Constructor of Component
  -- cmp(ID, CState, NState, TState) is a Component where the identifier is ID,
  --    the current local state is CState, the possible next local state is NState, and
  --    the target local state is TState
  op cmp : COID COState COState COState -> Component {constr}

  -- Selectors
  op id : Component -> COID
  op type : Component -> COType
  op state : Component -> COState
  op next : Component -> COState
  op target : Component -> COState
  var IDCO : COID
  vars CSCO NSCO TSCO : COState
  eq id(cmp(IDCO,CSCO,NSCO,TSCO)) = IDCO .
  eq state(cmp(IDCO,CSCO,NSCO,TSCO)) = CSCO .
  eq next(cmp(IDCO,CSCO,NSCO,TSCO)) = NSCO .
  eq target(cmp(IDCO,CSCO,NSCO,TSCO)) = TSCO .
}

module! COMPONENTfuns {
  -- Instantiation of Template
  extending(OBJECTBASE3(
	COMPONENT {sort Object -> Component,
		   sort ObjID -> COID,
		   sort ObjType -> COType,
		   sort ObjState -> COState,
		   op state2 -> next,
		   op state3 -> target})
	* {sort ObjIDLt -> COIDLt,
	   sort ObjTypeLt -> COTypeLt,
	   sort ObjStateLt -> COStateLt,
	   sort SetOfObject -> SetOfComponent,
	   sort SetOfObjState -> SetOfCOState,
	   op empObj -> empCO,
	   op empState -> empSCO,
	   op existObj -> existCO,
	   op existObjInStates -> existCOInStates,
	   op existObjInStates2 -> existCOInNextStates,
	   op existObjInStates3 -> existCOInTargetStates,
	   op uniqObj -> uniqCO,
	   op #ObjInStates -> #ComponentInStates,
	   op #ObjInStates2 -> #ComponentInNextStates,
	   op #ObjInStates3 -> #ComponentInTargetStates,
	   op getObject -> getComponent,
	   op allObjInStates -> allCOInStates,
	   op allObjInStates2 -> allCOInNextStates,
	   op allObjInStates3 -> allCOInTargetStates,
	   op allObjNotInStates -> allCONotInStates,
	   op allObjNotInStates2 -> allCONotInNextStates,
	   op allObjNotInStates3 -> allCONotInTargetStates,
	   op someObjInStates -> someCOInStates,
	   op someObjInStates2 -> someCOInNextStates,
	   op someObjInStates3 -> someCOInTargetStates})

  -- Local States
  -- "failed" is not concerned. 
  ops stopped started sstarted : -> COStateLt {constr}
}

module! IMPORT {
  protecting(COMPONENT)
  -- Sorts
  [Import IMID IMType IMState]

  -- Constructor of Import
  -- imp(IMType, ID, IID, EID, CState, NState, TState) is an Import
  --    where the type is IMType, the identifier is ID,
  --    the importer Component is IID, the exporter Component is EID,
  --    the current local state is CState, the next local state is NState, and
  --    the target local state is TState
  op imp : IMType IMID COID COID IMState IMState IMState -> Import {constr}

  -- Selectors
  op id : Import -> IMID
  op type : Import -> IMType
  op state : Import -> IMState
  op next : Import -> IMState
  op target : Import -> IMState
  op importer : Import -> COID
  op exporter : Import -> COID
  var TIM : IMType
  var IDIM : IMID
  vars IIDCO EIDCO : COID
  vars CSIM NSIM TSIM : IMState
  eq type(imp(TIM,IDIM,IIDCO,EIDCO,CSIM,NSIM,TSIM)) = TIM .
  eq id(imp(TIM,IDIM,IIDCO,EIDCO,CSIM,NSIM,TSIM)) = IDIM .
  eq state(imp(TIM,IDIM,IIDCO,EIDCO,CSIM,NSIM,TSIM)) = CSIM .
  eq next(imp(TIM,IDIM,IIDCO,EIDCO,CSIM,NSIM,TSIM)) = NSIM .
  eq target(imp(TIM,IDIM,IIDCO,EIDCO,CSIM,NSIM,TSIM)) = TSIM .
  eq importer(imp(TIM,IDIM,IIDCO,EIDCO,CSIM,NSIM,TSIM)) = IIDCO .
  eq exporter(imp(TIM,IDIM,IIDCO,EIDCO,CSIM,NSIM,TSIM)) = EIDCO .
}

module! IMPORTfuns {
  protecting(COMPONENTfuns)

  -- Instantiation of Template
  extending(OBJECTBASE3(
	IMPORT {sort Object -> Import,
		sort ObjID -> IMID,
		sort ObjType -> IMType,
		sort ObjState -> IMState,
		op state2 -> next,
		op state3 -> target})
	* {sort ObjIDLt -> IMIDLt,
	   sort ObjTypeLt -> IMTypeLt,
	   sort ObjStateLt -> IMStateLt,
	   sort SetOfObject -> SetOfImport,
	   sort SetOfObjState -> SetOfIMState,
	   op empObj -> empIM,
	   op empState -> empSIM,
	   op existObj -> existIM,
	   op existObjInStates -> existIMInStates,
	   op existObjInStates2 -> existIMInNextStates,
	   op existObjInStates3 -> existIMInTargetStates,
	   op uniqObj -> uniqIM,
	   op #ObjInStates -> #ImportInStates,
	   op #ObjInStates2 -> #ImportInNextStates,
	   op #ObjInStates3 -> #ImportInTargetStates,
	   op getObject -> getImport,
	   op allObjInStates -> allIMInStates,
	   op allObjInStates2 -> allIMInNextStates,
	   op allObjInStates3 -> allIMInTargetStates,
	   op allObjOfTypeInStates -> allIMOfTypeInStates,
	   op allObjOfTypeInStates2 -> allIMOfTypeInNextStates,
	   op allObjOfTypeInStates3 -> allIMOfTypeInTargetStates,
	   op allObjNotInStates -> allIMNotInStates,
	   op allObjNotInStates2 -> allIMNotInNextStates,
	   op allObjNotInStates3 -> allIMNotInTargetStates,
	   op someObjInStates -> someIMInStates,
	   op someObjInStates2 -> someIMInNextStates,
	   op someObjInStates3 -> someIMInTargetStates})

  -- Types
  ops mandatory optional : -> IMTypeLt

  -- Local States
  ops unused wired swired unwired : -> IMStateLt {constr}
}

module! LINKS {
  extending(OBJLINKMANY2ONE3(
	IMPORTfuns {sort Object -> Import,
		    sort ObjID -> IMID,
		    sort ObjType -> IMType,
		    sort ObjState -> IMState,
		    sort SetOfObject -> SetOfImport,
		    sort SetOfObjState -> SetOfIMState,
		    op empObj -> empIM,
		    op state2 -> next,
		    op state3 -> target,
		    op getObject(X:SetOfObject,Y:ObjID) 
		       -> getImport(X:SetOfImport,Y:IMID),
		    op existObj(X:SetOfObject,Y:ObjID)
		       -> existIM(X:SetOfImport,Y:IMID),
		    op allObjInStates(X:SetOfObject,Y:SetOfObjState)
		       -> allIMInStates(X:SetOfImport,Y:SetOfIMState),
		    op allObjInStates2(X:SetOfObject,Y:SetOfObjState)
		      -> allIMInNextStates(X:SetOfImport,Y:SetOfIMState),
		    op allObjInStates3(X:SetOfObject,Y:SetOfObjState)
		      -> allIMInTargetStates(X:SetOfImport,Y:SetOfIMState),
		    sort LObject -> Component,
		    sort LObjID -> COID,
		    sort LObjState -> COState,
		    sort SetOfLObject -> SetOfComponent,
		    sort SetOfLObjState -> SetOfCOState,
		    op link -> importer,
		    op empLObj -> empCO,
		    op getObject(X:SetOfLObject,Y:LObjID) 
		      -> getComponent(X:SetOfComponent,Y:COID),
		    op existObj(X:SetOfLObject,Y:LObjID)
		      -> existCO(X:SetOfComponent,Y:COID),
		    op allObjInStates(X:SetOfLObject,Y:SetOfLObjState)
		      -> allCOInStates(X:SetOfComponent,Y:SetOfCOState),
		    op allObjInStates2(X:SetOfLObject,Y:SetOfLObjState)
		      -> allCOInNextStates(X:SetOfComponent,Y:SetOfCOState),
		    op allObjInStates3(X:SetOfLObject,Y:SetOfLObjState)
 		      -> allCOInTargetStates(X:SetOfComponent,Y:SetOfCOState),
		    op existObjInStates -> existCOInStates,
		    op existObjInStates2 -> existCOInNextStates,
		    op existObjInStates3 -> existCOInTargetStates})
	* {op hasLObj -> hasImporter,
	   op getXOfZ -> getICOOfIM,
	   op getZsOfX -> getIMsOfICO,
	   op getZsOfTypeOfX -> getIMsOfTypeOfICO,
	   op getZsOfTypeOfXInStates -> getIMsOfTypeOfICOInStates,
	   op getZsOfTypeOfXInStates2 -> getIMsOfTypeOfICOInNextStates,
	   op getZsOfTypeOfXInStates3 -> getIMsOfTypeOfICOInTargetStates,
	   op getZsOfXInStates -> getIMsOfICOInStates,
	   op getZsOfXInStates2 -> getIMsOfICOInNextStates,
	   op getZsOfXInStates3 -> getIMsOfICOInTargetStates,
	   op getXsOfZs -> getICOsOfIMs,
	   op getXsOfZsInStates -> getICOsOfIMsInStates,
	   op getXsOfZsInStates2 -> getICOsOfIMsInNextStates,
	   op getXsOfZsInStates3 -> getICOsOfIMsInTargetStates,
	   op getZsOfXs -> getIMsOfICOs,
	   op getZsOfXsInStates -> getIMsOfICOsInStates,
	   op getZsOfXsInStates2 -> getIMsOfICOsInNextStates,
	   op getZsOfXsInStates3 -> getIMsOfICOsInTargetStates,
	   op allZHaveX -> allIMHaveICO,
	   op allZOfXInStates -> allIMOfICOInStates,
	   op allZOfXInStates2 -> allIMOfICOInNextStates,
	   op allZOfXInStates3 -> allIMOfICOInTargetStates,
	   op allZOfTypeOfXInStates -> allIMOfTypeOfICOInStates,
	   op allZOfTypeOfXInStates2 -> allIMOfTypeOfICOInNextStates,
	   op allZOfTypeOfXInStates3 -> allIMOfTypeOfICOInTargetStates,
	   op ifOfXThenInStates -> ifOfICOThenInStates,
	   op ifOfXThenInStates2 -> ifOfICOThenInNextStates,
	   op ifOfXThenInStates3 -> ifOfICOThenInTargetStates,
	   op ifXInStatesThenZInStates -> ifICOInStatesThenIMInStates,
	   op ifXInStatesThenZInStates2 -> ifICOInNextStatesThenIMInStates,
	   op ifXInStatesThenZInStates3 -> ifICOInTargetStatesThenIMInStates}
	)

  extending(OBJLINKMANY2ONE3(
	IMPORTfuns {sort Object -> Import,
		    sort ObjID -> IMID,
		    sort ObjType -> IMType,
		    sort ObjState -> IMState,
		    sort SetOfObject -> SetOfImport,
		    sort SetOfObjState -> SetOfIMState,
		    op empObj -> empIM,
		    op state2 -> next,
		    op state3 -> target,
		    op getObject(X:SetOfObject,Y:ObjID) 
		    -> getImport(X:SetOfImport,Y:IMID),
		    op existObj(X:SetOfObject,Y:ObjID)
		    -> existIM(X:SetOfImport,Y:IMID),
		    op allObjInStates(X:SetOfObject,Y:SetOfObjState)
		    -> allIMInStates(X:SetOfImport,Y:SetOfIMState),
		    op allObjInStates2(X:SetOfObject,Y:SetOfObjState)
		    -> allIMInNextStates(X:SetOfImport,Y:SetOfIMState),
		    op allObjInStates3(X:SetOfObject,Y:SetOfObjState)
		    -> allIMInTargetStates(X:SetOfImport,Y:SetOfIMState),
		    sort LObject -> Component,
		    sort LObjID -> COID,
		    sort LObjState -> COState,
		    sort SetOfLObject -> SetOfComponent,
		    sort SetOfLObjState -> SetOfCOState,
		    op link -> exporter,
		    op empLObj -> empCO,
		    op getObject(X:SetOfLObject,Y:LObjID) 
		      -> getComponent(X:SetOfComponent,Y:COID),
		    op existObj(X:SetOfLObject,Y:LObjID)
		      -> existCO(X:SetOfComponent,Y:COID),
		    op allObjInStates(X:SetOfLObject,Y:SetOfLObjState)
		      -> allCOInStates(X:SetOfComponent,Y:SetOfCOState),
		    op allObjInStates2(X:SetOfLObject,Y:SetOfLObjState)
		      -> allCOInNextStates(X:SetOfComponent,Y:SetOfCOState),
		    op allObjInStates3(X:SetOfLObject,Y:SetOfLObjState)
		      -> allCOInTargetStates(X:SetOfComponent,Y:SetOfCOState),
		    op existObjInStates -> existCOInStates,
		    op existObjInStates2 -> existCOInNextStates,
		    op existObjInStates3 -> existCOInTargetStates})
	* {op hasLObj -> hasExporter,
	   op getXOfZ -> getECOOfIM,
	   op getZsOfX -> getIMsOfECO,
	   op getZsOfTypeOfX -> getIMsOfTypeOfECO,
	   op getZsOfTypeOfXInStates -> getIMsOfTypeOfECOInStates,
	   op getZsOfTypeOfXInStates2 -> getIMsOfTypeOfECOInNextStates,
	   op getZsOfTypeOfXInStates3 -> getIMsOfTypeOfECOInTargetStates,
	   op getZsOfXInStates -> getIMsOfECOInStates,
	   op getZsOfXInStates2 -> getIMsOfECOInNextStates,
	   op getZsOfXInStates3 -> getIMsOfECOInTargetStates,
	   op getXsOfZs -> getECOsOfIMs,
	   op getXsOfZsInStates -> getECOsOfIMsInStates,
	   op getXsOfZsInStates2 -> getECOsOfIMsInNextStates,
	   op getXsOfZsInStates3 -> getECOsOfIMsInTargetStates,
	   op getZsOfXs -> getIMsOfECOs,
	   op getZsOfXsInStates -> getIMsOfECOsInStates,
	   op getZsOfXsInStates2 -> getIMsOfECOsInNextStates,
	   op getZsOfXsInStates3 -> getIMsOfECOsInTargetStates,
	   op allZHaveX -> allIMHaveECO,
	   op allZOfXInStates -> allIMOfECOInStates,
	   op allZOfXInStates2 -> allIMOfECOInNextStates,
	   op allZOfXInStates3 -> allIMOfECOInTargetStates,
	   op ifOfXThenInStates -> ifOfECOThenInStates,
	   op ifOfXThenInStates2 -> ifOfECOThenInNextStates,
	   op ifOfXThenInStates3 -> ifOfECOThenInTargetStates,
	   op ifXInStatesThenZInStates -> ifECOInStatesThenIMInStates,
	   op ifXInStatesThenZInStates2 -> ifECOInNextStatesThenIMInStates,
	   op ifXInStatesThenZInStates3 -> ifECOInTargetStatesThenIMInStates}
	)
}

module! LOG {
  protecting(IMPORT)
  protecting(STRING)
  -- transiton log for debugging, where Nat is m(S) .
  [Log]
  op clog : String Component Nat -> Log {constr}
  op ilog : String Import Nat -> Log {constr}
}

module! STATE {
  protecting(STATEBASE)
  protecting(LINKS)
  protecting(LIST(LOG {sort Elt -> Log})
      * {sort List -> ListOfLog,
      	 op empty -> empLog})

  -- A State consists of
  --  a set of Components, a set of Imports, and a list of logs
  op <_,_,_> : SetOfComponent SetOfImport ListOfLog -> State {constr}
}

module! STATECyclefuns {
  protecting(STATE)

  [LocalState]

  var CO : Component
  var SCO : SetOfComponent
  var SIM : SetOfImport
  var L : ListOfLog

  op getAllCO : State -> SetOfComponent
  eq getAllCO(< SCO,SIM,L >) = SCO .

  -- Upward DDSC
  op DDSCOU : Component State -> SetOfComponent
  eq DDSCOU(CO,< SCO,SIM,L >)
    = getECOsOfIMs(SCO,getIMsOfTypeOfICO(SIM,mandatory,CO)) .

  -- Downward DDSC
  op DDSCOD : Component State -> SetOfComponent
  eq DDSCOD(CO,< SCO,SIM,L >)
    = getICOsOfIMs(SCO,getIMsOfTypeOfECO(SIM,mandatory,CO)) .
}

module! STATEfuns {
  extending(CYCLEPRED(
	STATECyclefuns {sort Object -> Component,
			sort SetOfObject -> SetOfComponent,
			op empObj -> empCO,
			op DDSC -> DDSCOU,
			op getAllObjC -> getAllCO})
	* {op noCycleC -> noCOCycleU,
           op noCycleLemma -> noCycleLemmaU})

  extending(CYCLEPRED(
	STATECyclefuns {sort Object -> Component,
			sort SetOfObject -> SetOfComponent,
			op empObj -> empCO,
			op DDSC -> DDSCOD,
			op getAllObjC -> getAllCO})
	* {op noCycleC -> noCOCycleD,
           op noCycleLemma -> noCycleLemmaD})

  var CO : Component
  var IM : Import
  vars SCO SCO0 : SetOfComponent
  vars SIM SIM0 : SetOfImport
  var L : ListOfLog

  vars IDCO IIDCO EIDCO : COID 
  vars CSCO NSCO TSCO : COState
  var IDIM : IMID
  var TIM : IMType
  vars CSIM NSIM TSIM : IMState
  var StCO : SetOfCOState
  var StIM : SetOfIMState
  var S : State

  pred stableCOSet : SetOfComponent
  eq stableCOSet(empCO) = true .
  eq stableCOSet((cmp(IDCO,sstarted,NSCO,TSCO) SCO))
     = (NSCO = sstarted) and (TSCO = sstarted) and stableCOSet(SCO) .
  ceq stableCOSet((cmp(IDCO,CSCO,NSCO,TSCO) SCO))
     = (CSCO = NSCO) and stableCOSet(SCO)
     if not (CSCO = sstarted) .

  pred stableIMSet : SetOfImport
  eq stableIMSet(empIM) = true .
  eq stableIMSet((imp(TIM,IDIM,IIDCO,IDCO,swired,NSIM,TSIM) SIM))
     = (NSIM = swired) and (TSIM = swired) and stableIMSet(SIM) .
  ceq stableIMSet((imp(TIM,IDIM,IIDCO,IDCO,CSIM,NSIM,TSIM) SIM))
     = (CSIM = NSIM) and stableIMSet(SIM)
     if not (CSIM = swired) .

  pred equivCOSet : SetOfComponent
  eq equivCOSet(empCO) = true .
  eq equivCOSet(cmp(IDCO,CSCO,NSCO,TSCO) SCO)
     = (CSCO = NSCO) and (CSCO = TSCO) and equivCOSet(SCO) .

  pred equivIMSet : SetOfImport
  eq equivIMSet(empIM) = true .
  eq equivIMSet(imp(TIM,IDIM,IIDCO,IDCO,CSIM,NSIM,TSIM) SIM)
     = (CSIM = NSIM) and (CSIM = TSIM) and equivIMSet(SIM) .

  pred validTargetComponent : SetOfComponent SetOfComponent SetOfImport
  eq validTargetComponent(empCO,SCO0,SIM0) = true .
  eq validTargetComponent((cmp(IDCO,CSCO,NSCO,sstarted) SCO),SCO0,SIM0)
     = allIMInTargetStates(getIMsOfTypeOfICO(SIM0,mandatory,IDCO),(swired unused)) and
       allCOInTargetStates(getECOsOfIMs(SCO0,getIMsOfICOInTargetStates(SIM0,IDCO,swired)),sstarted) and
       validTargetComponent(SCO,SCO0,SIM0) .
  eq validTargetComponent((cmp(IDCO,CSCO,NSCO,stopped) SCO),SCO0,SIM0)
     = validTargetComponent(SCO,SCO0,SIM0) .
  eq validTargetComponent((cmp(IDCO,CSCO,NSCO,started) SCO),SCO0,SIM0)
     = false .

  pred validTargetImport : SetOfImport SetOfComponent
  eq validTargetImport(empIM,SCO0) = true .
  -- Assuming an Import swired to a sstarted Component.
  eq validTargetImport((imp(TIM,IDIM,IIDCO,EIDCO,CSIM,NSIM,swired) SIM),SCO0)
     = target(getComponent(SCO0,EIDCO)) = sstarted and validTargetImport(SIM,SCO0) .
  ceq validTargetImport((imp(TIM,IDIM,IIDCO,EIDCO,CSIM,NSIM,TSIM) SIM),SCO0)
     = validTargetImport(SIM,SCO0)
     if not (TSIM = swired) .

  pred validTarget : SetOfComponent SetOfImport
  eq validTarget(SCO,SIM)
     = validTargetComponent(SCO,SCO,SIM) and validTargetImport(SIM,SCO) .
  -- The no cycle invariant is assumed.
  -- ** and noCycleTarget(SCO,SIM) .

  pred validCurrentComponent : SetOfComponent SetOfComponent SetOfImport
  eq validCurrentComponent(empCO,SCO0,SIM0) = true .
  ceq validCurrentComponent((cmp(IDCO,CSCO,NSCO,TSCO) SCO),SCO0,SIM0)
     = allIMInStates(getIMsOfTypeOfICO(SIM0,mandatory,IDCO),(wired swired unused)) and
       allCOInStates(getECOsOfIMs(SCO0,getIMsOfICOInStates(SIM0,IDCO,(wired swired))),(started sstarted)) and
       validCurrentComponent(SCO,SCO0,SIM0) 
     if CSCO in (started sstarted) .
  eq validCurrentComponent((cmp(IDCO,stopped,NSCO,TSCO) SCO),SCO0,SIM0)
     = validCurrentComponent(SCO,SCO0,SIM0) .

  pred validCurrentImport : SetOfImport SetOfComponent
  -- "failed" is not concerned .
  eq validCurrentImport(SIM,SCO0) = true .

  pred validCurrent : SetOfComponent SetOfImport
  eq validCurrent(SCO,SIM)
     = validCurrentComponent(SCO,SCO,SIM) and validCurrentImport(SIM,SCO) .
  -- The no cycle invariant is assumed.
  -- ** and noCycleCurrent(SCO,SIM) .

  pred validNextComponent : SetOfComponent SetOfImport
  ceq validNextComponent((CO SCO),SIM)
    = not (next(CO) in stopped)
    if not (getIMsOfECOInStates(SIM,id(CO),swired) = empIM) .
  ceq validNextComponent((CO SCO),SIM)
    = true
    if getIMsOfECOInStates(SIM,id(CO),swired) = empIM .

  pred validNextImport : SetOfImport SetOfComponent
  eq validNextImport(empIM,SCO) = true .
  eq validNextImport(SIM,(cmp(IDCO,sstarted,NSCO,TSCO) SCO))
     = allIMInNextStates(getIMsOfTypeOfICO(SIM,mandatory,IDCO),(wired swired unused)) and
       validNextImport(SIM,SCO) .
  ceq validNextImport(SIM,(CO SCO))
     = validNextImport(SIM,SCO) 
     if not (state(CO) in sstarted) .
  eq validNextImport(SIM,empCO) = true .

  pred validNext : SetOfComponent SetOfImport
  eq validNext(SCO,SIM)
     = validNextComponent(SCO,SIM) and validNextImport(SIM,SCO) .
  -- The no cycle invariant is assumed.
  -- ** and noCycleNext(SCO,SIM) .

  pred iidCOeidCODiffer : SetOfImport
  eq iidCOeidCODiffer(empIM) = true .
  eq iidCOeidCODiffer(IM SIM)
    = not (importer(IM) = exporter(IM)) and iidCOeidCODiffer(SIM) .

  pred allCOInBothStates : SetOfComponent SetOfCOState 
  eq allCOInBothStates(empCO,StCO) = true .
  eq allCOInBothStates((cmp(IDCO,CSCO,NSCO,TSCO) SCO),StCO) 
     = (CSCO in StCO) and (NSCO in StCO) and allCOInBothStates(SCO,StCO) .

  pred allIMInBothStates : SetOfImport SetOfIMState 
  eq allIMInBothStates(empIM,StIM) = true .
  eq allIMInBothStates((imp(TIM,IDIM,IIDCO,IDCO,CSIM,NSIM,TSIM) SIM),StIM) 
     = (CSIM in StIM) and (NSIM in StIM) and allIMInBothStates(SIM,StIM) .

  pred allCOin9States : SetOfComponent
  -- allCOin9States is defined in Proof.cafe

  pred allIMin14States : SetOfImport
  -- allIMin14States is defined in Proof.cafe

  pred wfs-uniqCO : State
  eq wfs-uniqCO(< SCO,SIM,L >) = uniqCO(SCO) .

  pred wfs-uniqIM : State
  eq wfs-uniqIM(< SCO,SIM,L >) = uniqIM(SIM) .

  pred wfs-allIMHaveICO : State
  eq wfs-allIMHaveICO(< SCO,SIM,L >) = allIMHaveICO(SIM,SCO) .

  pred wfs-allIMHaveECO : State
  eq wfs-allIMHaveECO(< SCO,SIM,L >) = allIMHaveECO(SIM,SCO) .
  
  pred wfs-validTarget : State
  eq wfs-validTarget(< SCO,SIM,L >) = validTarget(SCO,SIM) .

  pred wfs-validCurrent : State
  eq wfs-validCurrent(< SCO,SIM,L >) = validCurrent(SCO,SIM) .

  pred wfs-validNext : State
  eq wfs-validNext(< SCO,SIM,L >) = validNext(SCO,SIM) .

  pred wfs-iidCOeidCODiffer : State
  eq wfs-iidCOeidCODiffer(< SCO,SIM,L >)
    = iidCOeidCODiffer(SIM) .

--> **************************************
--> Step 0-1: Define init(S) and final(S).
--> **************************************
  -- wfs is a conjunction of invariants which are intentionally imposed by the init condition.
  -- Since too long wfs causes a large evalutaion cost, define wfs minimally for each use case.
  eq [:nonexec]: wfs(S)
     = wfs-uniqCO(S) and wfs-uniqIM(S) and 
       wfs-allIMHaveICO(S) and wfs-allIMHaveECO(S) and
       wfs-validTarget(S) and wfs-validCurrent(S) and 
       wfs-validNext(S) and wfs-iidCOeidCODiffer(S) .

  eq init(< SCO,SIM,L >)
     = wfs(< SCO,SIM,L >) and
       allCOin9States(SCO) and
       allIMin14States(SIM) and
       stableCOSet(SCO) and stableIMSet(SIM) and
       not final(< SCO,SIM,L >) and
       noCOCycleU(< SCO,SIM,L >) and
       noCOCycleD(< SCO,SIM,L >) .

  eq final(< SCO,SIM,L >)
     = equivCOSet(SCO) and equivIMSet(SIM) .
}

-- Architecture Invariants
-- (1) All started components have their mandatory imports wired.
-- (2) All started components are wired only to started components.
-- (3) There are no wires to/from failed or destructed components.
-- (4) There are no cycles through mandatory imports

module! STATERules {
  protecting(STATEfuns)

  op mlog : SetOfComponent SetOfImport -> Nat

  -- Variables
  vars IDCO IIDCO EIDCO : COID 
  vars CSCO NSCO TSCO TSCO' : COState
  var IDIM : IMID
  var TIM : IMType
  vars CSIM NSIM TSIM : IMState

  var SCO : SetOfComponent -- current Components
  var SIM : SetOfImport    -- current Imports

  var ListLG : ListOfLog

  -- A started Component should be stopped if it is targeted.
  trans [RS-stop1]:
     < (cmp(IDCO, started, started, stopped) SCO), SIM,
       ListLG >
  => < (cmp(IDCO, started, stopped, stopped) SCO), SIM,
       (clog("RS-stop1:Should Stop", cmp(IDCO, started, stopped, stopped),
	  mlog(
       (cmp(IDCO, started, started, stopped) SCO), SIM
	 )) ListLG) > .

  -- A started Component can be stopped if there is no wired import from a started component to it.
  ctrans [RD-stop]:
     < (cmp(IDCO, started, stopped, TSCO) SCO), SIM,
       ListLG >
  => < (cmp(IDCO, stopped, stopped, TSCO) SCO), SIM,
       (clog("RD-stop:Do Stop", cmp(IDCO, stopped, stopped, TSCO),
	  mlog(
       (cmp(IDCO, started, stopped, TSCO) SCO), SIM
	 )) ListLG) >
     if getICOsOfIMsInStates(SCO,
                             getIMsOfECOInStates(SIM,IDCO,(wired swired)),
                             (started sstarted)) = empCO .

  -- A started Component should stop if its mandatory Import should be unwired.
  trans [RS-stop2]:
     < (cmp(IDCO, started, started, sstarted) SCO),
       (imp(mandatory, IDIM, IDCO, EIDCO, wired, unwired, TSIM) SIM),
        ListLG >
  => < (cmp(IDCO, started, stopped, sstarted) SCO),
       (imp(mandatory, IDIM, IDCO, EIDCO, wired, unwired, TSIM) SIM),
       (clog("RS-stop2:Should Stop", cmp(IDCO, started, stopped, sstarted),
	  mlog(
       (cmp(IDCO, started, started, sstarted) SCO),
       (imp(mandatory, IDIM, IDCO, EIDCO, wired, unwired, TSIM) SIM)
	 )) ListLG) > .

  -- A stopped Component should be stably started if it is targeted.
  trans [RS-sstart1]:
     < (cmp(IDCO, stopped,  stopped, sstarted) SCO), SIM,
       ListLG >
  => < (cmp(IDCO, stopped, sstarted, sstarted) SCO), SIM,
       (clog("RS-sstart1:Should Stably Start", cmp(IDCO, stopped, sstarted, sstarted),
	  mlog(
       (cmp(IDCO, stopped,  stopped, sstarted) SCO), SIM
	 )) ListLG) > .

  -- A started Component should be stably started 
  -- only if all of its mandatory imports are stalby wired or unused.
  ctrans [RS-sstart2]:
     < (cmp(IDCO, started,  started, sstarted) SCO), SIM,
       ListLG >
  => < (cmp(IDCO, started, sstarted, sstarted) SCO), SIM,
       (clog("RS-sstart2:Should Stably Start", cmp(IDCO, started, sstarted, sstarted),
	  mlog(
       (cmp(IDCO, started,  started, sstarted) SCO), SIM
	 )) ListLG) >
     if allIMInStates(getIMsOfTypeOfICO(SIM,mandatory,IDCO),(swired unused)) .

  -- A stopped Component can be started 
  -- only if all of its mandatory imports are stalby wired or unused.
  ctrans [RD-start]:
     < (cmp(IDCO, stopped, sstarted, sstarted) SCO), SIM,
       ListLG >
  => < (cmp(IDCO, started, sstarted, sstarted) SCO), SIM,
       (clog("RD-start:Do Start", cmp(IDCO, started, sstarted, sstarted),
	  mlog(
       (cmp(IDCO, stopped, sstarted, sstarted) SCO), SIM
	 )) ListLG) >
     if allIMInStates(getIMsOfTypeOfICO(SIM,mandatory,IDCO),(swired unused)) .

  -- A started Component can be stably started if it should be so.
  trans [RD-sstart]:
     < (cmp(IDCO,  started, sstarted, sstarted) SCO), SIM,
       ListLG >
  => < (cmp(IDCO, sstarted, sstarted, sstarted) SCO), SIM,
       (clog("RD-sstart:Do Stably Start", cmp(IDCO, sstarted, sstarted, sstarted),
	  mlog(
       (cmp(IDCO, started, sstarted, sstarted) SCO), SIM
	 )) ListLG) > .

  -- A wired Import should be unwired if it is targeted.
  trans [RS-unwire1]:
     < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, wired,   wired, unwired) SIM),
       ListLG >
  => < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, wired, unwired, unwired) SIM),
       (ilog("RS-unwire1:Should Unwire", imp(TIM, IDIM, IIDCO, EIDCO, wired, unwired, unwired),
	  mlog(
       SCO, (imp(TIM, IDIM, IIDCO, EIDCO, wired,   wired, unwired) SIM)
	 )) ListLG) > .

  -- An Optional Import can be unwired anytime.
  trans [RD-unwire1]:
     < SCO,
       (imp(optional, IDIM, IDCO, EIDCO,   wired, unwired, TSIM) SIM),
       ListLG >
  => < SCO,
       (imp(optional, IDIM, IDCO, EIDCO, unwired, unwired, TSIM) SIM),
       (ilog("RD-unwire1:Do Unwire", imp(optional, IDIM, IDCO, EIDCO, unwired, unwired, TSIM),
	  mlog(
       SCO,
       (imp(optional, IDIM, IDCO, EIDCO,   wired, unwired, TSIM) SIM)
	 )) ListLG) > .

  -- A Mandatory Import can be unwired only if its component is stopped.
  trans [RD-unwire2]:
     < (cmp(IDCO, stopped, NSCO, TSCO) SCO), 
       (imp(mandatory, IDIM, IDCO, EIDCO,   wired, unwired, TSIM) SIM),
       ListLG >
  => < (cmp(IDCO, stopped, NSCO, TSCO) SCO),
       (imp(mandatory, IDIM, IDCO, EIDCO, unwired, unwired, TSIM) SIM),
       (ilog("RD-unwire2:Do Unwire", imp(mandatory, IDIM, IDCO, EIDCO, unwired, unwired, TSIM),
	  mlog(
       (cmp(IDCO, stopped, NSCO, TSCO) SCO), 
       (imp(mandatory, IDIM, IDCO, EIDCO,   wired, unwired, TSIM) SIM)
	 )) ListLG) > .

  -- A wired Import should be unused if it is targeted.
  trans [RS-unused]:
     < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, wired,  wired, unused) SIM),
       ListLG >
  => < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, wired, unused, unused) SIM),
       (ilog("RS-unused:Should be Unused", imp(TIM, IDIM, IIDCO, EIDCO, wired, unused, unused),
	  mlog(
       SCO, (imp(TIM, IDIM, IIDCO, EIDCO, wired, wired, unused) SIM)
	 )) ListLG) > .

  -- A wired Import can be unused if it should be so.
  trans [RD-unused]:
     < SCO, (imp(TIM, IDIM, IDCO, EIDCO,  wired, unused, unused) SIM),
       ListLG >
  => < SCO, (imp(TIM, IDIM, IDCO, EIDCO, unused, unused, unused) SIM),
       (ilog("RD-unused:Do Unwire", imp(TIM, IDIM, IDCO, EIDCO, unused, unused, unused),
	  mlog(
       SCO, (imp(TIM, IDIM, IDCO, EIDCO,  wired, unused, unused) SIM)
	 )) ListLG) > .

  -- An Unused Import should be firstly unwired if it will be stably wired.
  trans [RS-unwire2]:
     < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, unused,  unused, swired) SIM),
       ListLG >
  => < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, unused, unwired, swired) SIM),
       (ilog("RS-unwire2:Should Unwire", imp(TIM, IDIM, IIDCO, EIDCO, unused, unwired, swired),
	  mlog(
       SCO, (imp(TIM, IDIM, IIDCO, EIDCO, unused,  unused, swired) SIM)
	 )) ListLG) > .

  -- An Unused Import can be unwired anytime.
  trans [RD-unwire3]:
     < SCO, (imp(TIM, IDIM, IIDCO, EIDCO,  unused, unwired, swired) SIM),
       ListLG >
  => < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, unwired, unwired, swired) SIM),
       (ilog("RD-unwire3:Do Unwire", imp(TIM, IDIM, IIDCO, EIDCO, unwired, unwired, swired),
	  mlog(
       SCO, (imp(TIM, IDIM, IIDCO, EIDCO,  unused, unwired, swired) SIM)
	 )) ListLG) > .

  -- An unwired Import should be stably wired if it is targeted.
  trans [RS-swire1]:
     < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, unwired, unwired, swired) SIM),
       ListLG >
  => < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, unwired,  swired, swired) SIM),
       (ilog("RS-swire1:Should Wire", imp(TIM, IDIM, IIDCO, EIDCO, unwired, swired, swired),
	  mlog(
       SCO, (imp(TIM, IDIM, IIDCO, EIDCO, unwired, unwired, swired) SIM)
	 )) ListLG) > .

  -- A wired Import should be stably wired if its export Component is stably started.
  trans [RS-swire2]:
     < (cmp(EIDCO, sstarted, sstarted, sstarted) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, wired,  wired, swired) SIM),
       ListLG >
  => < (cmp(EIDCO, sstarted, sstarted, sstarted) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, wired, swired, swired) SIM),
       (ilog("RS-swire2:Should Wire", imp(TIM, IDIM, IIDCO, EIDCO, wired, swired, swired),
	  mlog(
       (cmp(EIDCO, sstarted, sstarted, sstarted) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, wired, wired, swired) SIM)
       )) ListLG) > .

  -- An Import should be unwired if its export Component should be stopped.
  trans [RS-unwire3]:
     < (cmp(EIDCO, started, stopped, sstarted) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, wired,   wired, swired) SIM),
       ListLG >
  => < (cmp(EIDCO, started, stopped, sstarted) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, wired, unwired, swired) SIM),
       (ilog("RS-unwire3:Should Unwire", imp(TIM, IDIM, IIDCO, EIDCO, wired, unwired, swired),
	  mlog(
       (cmp(EIDCO, started, stopped, sstarted) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, wired,   wired, swired) SIM)
	 )) ListLG) > .

  -- A unwired Import can be wired if its Component is stopped.
  trans [RD-wire1]:
     < (cmp(IIDCO, stopped, stopped, TSCO) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, unwired, swired, swired) SIM),
       ListLG >
  => < (cmp(IIDCO, stopped, stopped, TSCO) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO,   wired, swired, swired) SIM),
       (ilog("RD-wire1:Do Wire", imp(TIM, IDIM, IIDCO, EIDCO, wired, swired, swired),
	  mlog(
       (cmp(IIDCO, stopped, stopped, TSCO) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, unwired, swired, swired) SIM)
	 )) ListLG) > .

  -- A unwired Import can be wired if its export Component is stably started.
  trans [RD-wire2]:
     < (cmp(EIDCO, sstarted, sstarted, sstarted) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, unwired, swired, swired) SIM),
       ListLG >
  => < (cmp(EIDCO, sstarted, sstarted, sstarted) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO,   wired, swired, swired) SIM),
       (ilog("RD-wire2:Do Wire", imp(TIM, IDIM, IIDCO, EIDCO, wired, swired, swired),
	  mlog(
       (cmp(EIDCO, sstarted, sstarted, sstarted) SCO), 
       (imp(TIM, IDIM, IIDCO, EIDCO, unwired, swired, swired) SIM)
	 )) ListLG) > .

  -- A wired Import can be stably wired if it should be so.
  trans [RD-swire]:
     < SCO, (imp(TIM, IDIM, IIDCO, EIDCO,  wired, swired, swired) SIM),
       ListLG >
  => < SCO, (imp(TIM, IDIM, IIDCO, EIDCO, swired, swired, swired) SIM),
       (ilog("RD-swire:Do Stably Wire", imp(TIM, IDIM, IIDCO, EIDCO, swired, swired, swired),
	  mlog(
       SCO, (imp(TIM, IDIM, IIDCO, EIDCO,  wired, swired, swired) SIM)
	 )) ListLG) > .

}
